/* DECORATE actors for HACX */

actor HacxPlayer : PlayerPawn
{
	Speed 1
	Health 100
	Radius 16
	Height 56
	Mass 100
	Scale 0.9
	PainChance 255
	Player.ColorRange 112, 127
	Player.DisplayName "Hacker"
	Player.CrouchSprite "PLYC"
	Player.ViewHeight 56
	Player.AttackZOffset 28
	player.startitem "HacxLightKevlarVest"
	player.startitem "HacxPistol"
	player.startitem "HacxRounds", 100
	player.startitem "HacxKick", 1
	
	Player.Weaponslot 1, HacxKick, HacxReznator
	Player.Weaponslot 2, HacxPistol
	Player.Weaponslot 3, HacxTazer, HacxCryogun
	Player.Weaponslot 4, HacxUzi, HacxDualUzis
	Player.Weaponslot 5, HacxPhotonZooka
	Player.Weaponslot 6, HacxAntigun
	Player.Weaponslot 7, HacxNuker
	
	States
	{
	Spawn:
		PLAY A -1
		Loop
	See:
		PLAY BCDE 4 
		Loop
	Missile:
//		PLAY F 0 A_JumpIfInventory("KickToken", 1,"Kick")
		PLAY F 8
		Goto Spawn
	Melee:
		PLAY G 2 BRIGHT
		PLAY F 2
		Goto Missile
//	Kick:
//    	PLAY C 6
//    	PLAY H 8 
//    	PLAY C 7 
//    	Goto Spawn
	Pain:
		PLAY H 4 
		PLAY H 4 A_Pain
		Goto Spawn
	Pain.Ramming:
		PLAY A 0 A_Recoil(10.0)
		Goto Pain
	Death:
		PLAY I 6
		PLAY J 5 A_PlayerScream
		PLAY K 4 A_NoBlocking
		PLAY L 4
		PLAY MN 4
		PLAY O -1
		Stop
	XDeath:
		PLAY P 4
		PLAY Q 4 A_XScream
		PLAY R 4 A_NoBlocking
		PLAY ST 4
		PLAY U -1
		Stop
	}
}
//actor KickToken : Inventory {}
actor HacxPlayerDummy : HacxPlayer
{
	speed 16
	painchance 100
	painsound "*pain"
	deathsound "*death"
	MONSTER
	+FLOORCLIP
	-PICKUP
	States
	{
	Spawn:
		PLAY A 10 A_Look
		Loop
	See:
		PLAY BBCCDDEE 2 A_Wander
		Loop
	Pain:
		PLAY A 4 
		PLAY A 4 A_Pain
		Goto Spawn
	Melee:
	Missile:
		Stop
	}
}

				      /* ---------- WEAPONS ---------- */
					  
actor HacxWeapon : Weapon
{
	//game Hacx
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 2.0
	Weapon.BobRangeX 0.5
	Weapon.BobRangeY 0.75
	Weapon.Kickback 100
	States
	{
		Spawn:
			NOPE A 1
			Stop
		Ready:
			NOPE A 1 A_WeaponReady
			Loop
		Deselect:
			NOPE A 1 A_Lower
			Loop
		Select:
			NOPE A 1 A_Raise
			Loop
		Fire:
			NOPE A 1
			Goto Ready
		AltFire:
			KIK2 A 0
		Kick:
//			"####" A 0 A_GiveInventory("KickToken",1) 
			"####" A 2 A_PlayWeaponSound("weapons/grunt")
			"####" B 2
			"####" B 0 A_JumpIfInventory("PowerStrength",1,"KickSuper")
			"####" C 8 A_CustomPunch(4*random(1,10),1,0,"KickPuff",96)
		KickFinish:
			"####" BA 2 //A_TakeInventory("KickToken",1)
			"####" B 0 A_Jump(256, "Idle")
		Idle:
		IdleWait:
			"####" AAAAAAAAAA 1 A_WeaponReady(WRF_NOSECONDARY | WRF_ALLOWRELOAD)
			"####" A 0 A_Jump(256, "Ready")
			Goto Ready
		KickSuper:
			"####" C 8 A_CustomPunch(40*random(1,10),1,0,"KickPuff",96)
			goto KickFinish
			
		// QuickKick. [XA] Hopefully this'll be an "underlay" PSprite eventually.
		/*
		AltFire:
			KICK E 0 A_GiveInventory("KickToken",1) 
			KICK E 0 A_GunFlash("Idle", GFF_NOEXTCHANGE)
			KICK E 3 A_PlayWeaponSound("weapons/grunt")
			KICK F 0 A_JumpIfInventory("PowerStrength",1,"KickSuper")
			KICK F 8 A_CustomPunch(4*random(1,10),1,0,"KickPuff",96)
		KickFinish:
			KICK E 3 A_TakeInventory("KickToken",1) 
			TNT1 AAAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOSECONDARY | WRF_ALLOWRELOAD)
			TNT1 A 0 A_Jump(256, "Ready")
			Goto Ready
		KickSuper:
			KICK F 8 A_CustomPunch(40*random(1,10),1,0,"KickPuff",96)
			goto KickFinish
			
		Idle:
			TNT1 A 0
		IdleWait:
			"####" "#" 30
			Stop
		*/
	}
}


actor HacxProjectile
{
	Radius 13
	Height 8
	Projectile
}

actor HacxTrail
{
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
}

actor HacxPuff : BulletPuff replaces BulletPuff
{
	States
	{
	Spawn:
		PUFF A 3 bright
		PUFF B 3
	Melee:
		PUFF CD 3
		Stop
	}
}

// Kick ---------------------------------------------------------------------

actor HacxKick : HacxWeapon
{
	//game Hacx
	Weapon.SelectionOrder 3700
	Obituary "$OB_MPKICK"
	Tag "$TAG_KICK"
	+WEAPON.NOALERT
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	states
	{
		Ready:
			KICK A 1 A_WeaponReady
			Loop
		Deselect:
//			KICK A 0 A_TakeInventory("KickToken",1)
			KICK A 0 A_Lower
			KICK A 1 A_Lower
			Goto Deselect+1
		Select:
//			KICK A 0 A_GiveInventory("KickToken",1)
			KICK A 0 A_Raise
			KICK A 1 A_Raise
			Goto Select+1
		AltFire:		
		Fire:
			KICK B 3 A_PlayWeaponSound("weapons/grunt")
			KICK C 3 
			KICK D 0 A_JumpIfInventory("PowerStrength",1,"KickSuper")
			KICK D 8 A_CustomPunch(4*random(1,10),1,0,"KickPuff",96)
		KickFinish:
			KICK C 3 
			KICK B 3
			KICK A 0 A_ReFire
			goto Ready
		KickSuper:
			KICK D 8 A_CustomPunch(40*random(1,10),1,0,"KickPuff",96)
			goto KickFinish
	}
}
actor KickPuff : HacxPuff
{
	+PUFFONACTORS
	DamageType "Kick"
	SeeSound "weapons/kick"
	ProjectileKickBack 500
}

// Pistol -------------------------------------------------------------------

actor HacxPistol : HacxWeapon replaces Pistol
{
	//game Hacx
	Weapon.SelectionOrder 1900
	Weapon.UpSound "weapons/pistolup"
	Weapon.AmmoUse 2
	Weapon.AmmoGive 20
	Weapon.AmmoType "HacxRounds"
	Inventory.Pickupmessage "$GOTXPISTOL"
	Obituary "$OB_MPXPISTOL"
	Tag "$TAG_XPISTOL"
	+WEAPON.WIMPY_WEAPON
	states
	{
		Spawn:
			PIST A -1
			Stop
		Ready:
			PISG A 1 A_WeaponReady
			Loop
		Idle:
			PISG A 0
			Goto IdleWait
		Deselect:
			PISG A 0 A_Lower
			PISG A 1 A_Lower
			Loop
		Select:
			PISG A 0 A_Raise
			PISG A 1 A_Raise
			Loop
		Fire:
			PISG A 0 A_GunFlash
			PISG B 0 A_PlayWeaponSound("weapons/pistol")
			PISG B 5 A_FireBullets (2, 0, 2, 5, "HacxPuff")
			PISG CA 3
			PISG A 4 A_ReFire
			goto Ready
		Flash:
			PISF A 3 bright A_Light1
			PISF A 0 bright A_Light0
			stop
		AltFire:
			PISK A 0
			Goto Kick
	}
}

// Reznator -----------------------------------------------------------------

actor HacxReznator : HacxWeapon replaces Chainsaw
{
	//game Hacx
	SpawnID 32
	Weapon.Kickback 0
	Weapon.SelectionOrder 2200
	Weapon.UpSound "weapons/rezup"
	Weapon.ReadySound "weapons/rezidle"
	Inventory.PickupMessage "$GOTREZNATOR"
	Obituary "$OB_MPREZNATOR"
	Tag "$TAG_REZNATOR"
	+WEAPON.MELEEWEAPON	
	States
	{
		Spawn:
			REZN A -1
			Stop
		Ready:
			REZG CD 4 A_WeaponReady
			Loop
		Idle:
			REZG D 2 A_WeaponReady(WRF_NOSECONDARY)
			REZG C 0 A_PlaySound("weapons/rezidle")
			REZG CD 4 A_WeaponReady(WRF_NOSECONDARY)
			Goto Ready
		Deselect:
			REZG C 0 A_StopSound(CHAN_WEAPON)
			REZG C 0 A_PlaySound("weapons/rezdown", CHAN_AUTO)
		DeselectLoop:
			REZG C 0 A_Lower
			REZG C 1 A_Lower
			Loop
		Select:
			REZG C 0 A_Raise
			REZG C 1 A_Raise
			REZG C 0 A_Raise
			REZG C 1 A_Raise
			REZG C 0 A_Raise
			REZG C 1 A_Raise
			REZG C 0 A_Raise
			REZG C 1 A_Raise
			REZG C 0 A_Raise
			REZG C 1 A_Raise
			REZG D 0 A_Raise
			REZG D 1 A_Raise
			REZG D 0 A_Raise
			REZG D 1 A_Raise
			REZG D 2 A_WeaponReady
			REZG CD 4 A_WeaponReady
			Goto Ready
		Fire:
			REZG A 0 A_GunFlash("LightDone", GFF_NOEXTCHANGE)
			REZG AB 3 bright A_Saw("weapons/rezfull","weapons/rezhit",2,"ReznatorPuff")
			REZG B 0 A_ReFire
			goto Ready
		AltFire:
			REZK A 0
			Goto Kick
	}
}

Actor ReznatorPuff : HacxPuff
{
	+PUFFONACTORS
	DamageType Reznator
	RenderStyle Add
	Alpha 0.8
	VSpeed 0
	States
	{
	Spawn:
	Melee:
		RZAP ACEGH 2 bright
		Stop
	}	
}

// Tazer --------------------------------------------------------------------

actor HacxTazer : HacxWeapon replaces Shotgun
{
	//game Hacx
	spawnid 27
	Weapon.SelectionOrder 1300
	Weapon.UpSound "weapons/tazerup"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	weapon.ammotype "HacxCartridges"
	inventory.pickupmessage "$GOTTAZER"
	obituary "$OB_MPTAZER"
	Tag "$TAG_TAZER"
	states
	{
		Spawn:
			TAZR A -1
			Stop
		Idle:
			TAZG A 0
			Goto IdleWait
		Ready:
			TAZG A 1 A_WeaponReady
			loop
		Deselect:
			TAZG A 0 A_Lower
			TAZG A 1 A_Lower
			loop
		Select:
			TAZG A 0 A_Raise
			TAZG A 1 A_Raise
			loop
		Fire:
			TAZG B 0 A_PlaySound("weapons/tazercharge")
			TAZG A 3 A_GunFlash
			TAZG A 0 A_PlayWeaponSound("weapons/tazer")
			TAZF A 3 bright A_FireCustomMissile ("TazerShot")
			TAZF B 2 bright
			TAZG C 2
			TAZG D 3
			TAZG E 5
			TAZG D 4
			TAZG C 3
			TAZG A 4
			TAZG A 1 A_ReFire
			goto Ready
		Flash:
			TAZC A 2 bright 
			TAZC B 2 bright A_Light1
			TAZC C 2 bright A_Light2
			TNT1 A 2
			TNT1 A 0 A_Light0
			stop
		AltFire:
			TAZK A 0
			Goto Kick
	}
}

actor TazerShot : HacxProjectile
{
	//game Hacx
	Speed 50
	damage 10
	seesound "weapons/tazerzap"
	deathsound "weapons/tazerhit"
	obituary "$OB_MPTAZER"
	decal "TazerScorch"
	RenderStyle "Add"
	Alpha 0.75
	states
	{
		Spawn:
			TBAL AAABBBCCC 1 bright A_SpawnItemEx("TazerTrail1",0,0.1*random(-10,10),0.1*random(-10,10)-2,0,0,0,0,SXF_CLIENTSIDE,0)
			loop
		Death:
			TPUF AB 3 bright
			TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0, 0   + random(-5,5),SXF_TRANSFERPOINTERS,0)
			TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0, 72  + random(-5,5),SXF_TRANSFERPOINTERS,0)
			TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0, 144 + random(-5,5),SXF_TRANSFERPOINTERS,0)
			TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0, 216 + random(-5,5),SXF_TRANSFERPOINTERS,0)
			TPUF C 0 A_SpawnItemEx("TazerFrag",0,0,0,20,0,0, 288 + random(-5,5),SXF_TRANSFERPOINTERS,0)
			TPUF CDE 3 bright
			stop
	}
}

actor TazerFrag : HacxProjectile
{
	//game Hacx
	speed 20
	damage 6
	seesound "weapons/tazerzap"
	obituary "$OB_MPTAZER"
	decal "TazerScorch"
	states
	{
		Spawn:
			TPUF AABBBAA 1 bright A_SpawnItemEx("TazerTrail2",0,0.5*random(-10,10),0.5*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			stop
		XDeath:
			TPUF C 0 A_PlaySound("weapons/tazerfire")
		Death:
			TPUF CDE 2 bright
			stop
	}
}

Actor TazerTrail1 : HacxTrail
{
	Alpha 0.5
	Scale 0.75
	Renderstyle Add
	States
	{
	Spawn:
		TZAP EEEDDDCCCBBBAAA 1 bright A_FadeOut(0.05)
		Stop
	}	
}

Actor TazerTrail2 : HacxTrail
{
	Alpha 1
	Renderstyle Add
	States
	{
	Spawn:
		TZAP AABBAA 1 bright A_FadeOut(0.10)
		Stop
	}	
}

// Cryogun ------------------------------------------------------------------

actor HacxCryogun : HacxWeapon replaces SuperShotgun
{
	//game Hacx
	spawnid 33
	Weapon.SelectionOrder 400
	Weapon.UpSound "weapons/cryoup"
	Weapon.AmmoUse 2
	Weapon.AmmoGive 8
	weapon.ammotype "HacxCartridges"
	inventory.pickupmessage "$GOTCRYOGUN"
	obituary "$OB_MPCRYOGUN"
	Tag "$TAG_CRYOGUN"
	states
	{
		Spawn:
			CRYO A -1
			Stop
		Ready:
			CRYG A 1 A_WeaponReady
			loop
		Idle:
			CRYG A 0
			Goto IdleWait
		Deselect:
			CRYG A 0 A_Lower
			CRYG A 1 A_Lower
			loop
		Select:
			CRYG A 0 A_Raise
			CRYG A 1 A_Raise
			loop
		Fire:
			CRYG A 2
			CRYG A 0 A_GunFlash
			CRYG A 0 A_PlayWeaponSound ("weapons/cryofire")
			CRYG AAAAA 0 A_FireCustomMissile("CryoShot",0.1*random(-80,80),0,0,0,1,0.1*random(-20,20))
			CRYG AAAA 0  A_FireCustomMissile("CryoShot",0.1*random(-65,65),0,0,0,1,0.1*random(-30,30))
			CRYG AAAA 0  A_FireCustomMissile("CryoShot",0.1*random(-50,50),0,0,0,1,0.1*random(-40,40))
			CRYG AAAA 0  A_FireCustomMissile("CryoShot",0.1*random(-35,35),0,0,0,1,0.1*random(-50,50))
			CRYG A 4     A_FireCustomMissile("CryoShot",0.1*random(-35,35),1,0,0,1,0.1*random(-20,20))
//			CRYG A 4 A_FireCustomMissile("CryoSpread",0,1)
			CRYG B 3
			CRYG C 3 A_CheckReload
			CRYG D 4 A_PlayWeaponSound ("weapons/uzireload")
			CRYG E 5
			CRYG F 4 A_PlayWeaponSound ("weapons/cryohit")
			CRYG G 4
			CRYG H 0 A_PlayWeaponSound ("weapons/cryocool")
			CRYG H 3 A_ReFire
			goto Ready
		Flash:
			CRYG I 2 BRIGHT A_Light1
			CRYG J 3 BRIGHT A_Light0
			stop
		AltFire:
			CRYK A 0
			Goto Kick
	}
}

/*
Actor CryoSpread : HacxProjectile
{
	Damage 0
	Speed 120
	+THRUACTORS
	States
	{
	Spawn:
		CPRJ A 0 bright
		TNT1 A 0 bright A_SpawnItemEx("CryoShot",0,0,0,velx,vely,velz,angle,SXF_TRANSFERPOINTERS)
	Death:
		TNT1 A 0 bright
		Stop
	}
}
*/

ACTOR CryoShot : FastProjectile
{
	Damage (5*random(1,3)) // same as standard Doom hitscan
	Radius 8
	Height 8
	Speed 120
	RenderStyle Add
	SeeSound ""
	DeathSound "weapons/cryohit"
	Obituary "$OB_MPCRYOGUN"
	Decal "CryoScorch"
	States
	{
	Spawn:
		CPRJ A 1 bright A_SpawnItemEx("CryoTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		Loop
	Death:
		CPUF ABCDEFG 3 bright
		Stop
	}
}

Actor CryoTrail : HacxTrail
{
	Alpha 0.25
	Scale 0.5
	Renderstyle Add
	States
	{
	Spawn:
		CPUF CDCD 3 bright A_FadeOut(0.05)
		Stop
	}	
}

// Uzi ----------------------------------------------------------------------

actor HacxUzi : HacxWeapon replaces Chaingun
{
	//game Hacx
	SpawnId 28
	Weapon.SelectionOrder 700
	Weapon.UpSound "weapons/uziup"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "HacxRounds"
	Weapon.AmmoType2 "HacxRounds"
	Inventory.PickupMessage "$GOTUZI"
	Obituary "$OB_MPUZI"
	Tag "$TAG_UZI"
	states
	{
		Spawn:
			UZII A -1
			Stop
		Ready:
			UZIG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop
		Idle:
			UZIG A 0
			Goto IdleWait
		Deselect:
			UZIG A 0 A_Lower
			UZIG A 1 A_Lower
			loop
		Select:
			UZIG A 0 A_Raise
			UZIG A 1 A_Raise
			loop
		Fire:
		Hold:
			UZIF A 0 A_GunFlash
			UZIF A 0 A_PlayWeaponSound("weapons/pistol")
			UZIF A 2 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZIF B 0 A_PlayWeaponSound("weapons/pistol")
			UZIF B 2 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZIG B 0 A_CheckForReload (25, "Refire")
			// intentional fall-through
		DoReload:
			UZIG B 3 A_JumpIfNoAmmo("Ready")
			UZIG C 3 
			UZIG D 3 A_PlayWeaponSound("weapons/uzireload")
			UZIG E 8 A_ResetReloadCounter
			UZIG CBA 3
			// ditto
		Refire:
			UZIG B 0 A_ReFire
			goto Ready
		Reload:
			UZIG A 0 A_GunFlash("LightDone", GFF_NOEXTCHANGE)
			UZIG A 1 A_WeaponReady(WRF_NOFIRE)
			UZIG A 0 A_CheckForReload (1, "Ready", 1)
			goto DoReload
		Flash:
			TNT1 A 2 A_Light1
			TNT1 A 2 A_Light2
			TNT1 A 0 A_Light0
			stop
		AltFire:
			UZIK A 0
			Goto Kick
	}
}

/*
actor HacxUziAlt : HacxUzi
{
	states
	{
		Ready:
			UZI1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop
		Deselect:
			UZI1 A 1 A_Lower
			loop
		Select:
			UZI1 A 1 A_Raise
			loop
		Fire:
		Hold:
			UZI1 B 0 A_GunFlash 
			UZI1 B 2 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI1 C 2 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI1 C 0 A_CheckForReload (25, "Refire")
			// intentional fall-through
		DoReload:
			UZI1 A 6 A_JumpIfNoAmmo("Ready")
			UZIG C 3
			UZIG D 3 A_ResetReloadCounter
			UZIG E 11 A_PlayWeaponSound("weapons/uzireload")
			UZI1 A 6
			// ditto
		Refire:
			UZI1 A 0 A_ReFire
			goto Ready
		Reload:
			UZI1 A 1 A_WeaponReady(WRF_NOFIRE)
			UZI1 A 0 A_CheckForReload (1, "Ready", 1)
			goto DoReload
	}
}
*/

// Dual Uzis ----------------------------------------------------------------------

actor HacxDualUzis : HacxUzi
{
	obituary "$OB_MPDUALUZIS"
	inventory.pickupmessage "$GOTDUALUZIS"
	Tag "$TAG_DUALUZIS"
	+CHEATNOTWEAPON // Temporary for now, since they muck with my "Give Weapons" preference :P
	states
	{
		Spawn:
			UZII A -1
			Stop
		Ready:
			UZI2 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop
		Idle:
			UZI2 A 0
			Goto IdleWait
		Deselect:
			UZI2 A 0 A_Lower
			UZI2 A 1 A_Lower
			loop
		Select:
			UZI2 A 0 A_Raise
			UZI2 A 1 A_Raise
			loop
		Fire:
		Hold:
			UZI2 B 0 A_GunFlash 
			UZI2 A 0 A_PlaySound("weapons/pistol")
			UZI2 B 0 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI2 B 1 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI2 B 1 A_PlaySound("weapons/pistol",CHAN_WEAPON)
			UZI2 C 0 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI2 C 2 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI2 C 0 A_CheckForReload (25, 1)
			goto DoReload
			UZI2 C 0 A_JumpIfNoAmmo("Ready")
			UZI2 C 0 A_PlaySound("weapons/pistol")
			UZI2 D 0 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI2 D 1 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI2 D 1 A_PlaySound("weapons/pistol",CHAN_WEAPON)
			UZI2 E 0 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI2 E 2 bright A_FireBullets (5.6, 0, -1, 5, "HacxPuff")
			UZI2 C 0 A_CheckForReload (25, "Refire")
			// intentional fall-through
		DoReload:
			UZI2 F 3 A_JumpIfNoAmmo("Ready")
			UZI2 GH 3
			UZIG D 3 A_PlayWeaponSound("weapons/uzireload")
			UZIG E 8 A_ResetReloadCounter
			UZIG CB 3
			UZI2 I 3
			UZI2 JK 3
			UZI2 L 3 A_PlayWeaponSound("weapons/uzireload")
			UZI2 M 8
			UZI2 NOP 3
			// ditto
		Refire:
			UZI2 A 0 A_ReFire
			goto Ready
		Reload:
			UZI2 A 1 A_WeaponReady(WRF_NOFIRE)
			UZI2 A 0 A_CheckForReload (1, "Ready", 1)
			goto DoReload
		AltFire:
			UZ2K A 0
			Goto Kick
	}
}

// Photon 'Zooka ------------------------------------------------------------

actor HacxPhotonZooka : HacxWeapon replaces RocketLauncher
{
	//game Hacx
	spawnid 29
	Weapon.SelectionOrder 2500
	Weapon.UpSound "weapons/zookaup"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "HacxTorpedo"
	inventory.pickupmessage "$GOTPHOTONZOOKA"
	Tag "$TAG_PHOTONZOOKA"
	+WEAPON.EXPLOSIVE
	+WEAPON.NOAUTOFIRE
	states
	{
		Spawn:
			ZOOK A -1
			Stop
		Ready:
			PHZG A 1 A_WeaponReady
			loop
		Idle:
			PHZG A 0
			Goto IdleWait
		Deselect:
			PHZG A 0 A_Lower
			PHZG A 1 A_Lower
			loop
		Select:
			PHZG A 0 A_Raise
			PHZG A 1 A_Raise
			loop
		Fire:
			PHZF A 4 bright A_GunFlash
			PHZF B 0 bright A_PlayWeaponSound("weapons/zookafire")
//			PHZF B 4 bright A_FireCustomMissile("HacxPhoton",0,1,0,0,2,5)
			PHZF B 4 bright A_FireCustomMissile("HacxPhoton",0,1)
			PHZF CD 4 bright
			PHZG B 4 bright
			PHZG B 3 bright A_ReFire
			goto Ready
		Flash:
			TNT1 A 3 A_Light1
			TNT1 A 3 A_Light2
			TNT1 A 3 A_Light1
			TNT1 A 0 A_Light0
			stop
		AltFire:
			PHZK A 0
			Goto Kick
	}
}

actor HacxPhoton : HacxProjectile
{
	//game Hacx
	spawnid 127
	obituary "$OB_MPZOOKA"
	speed 35
	damage 20
	scale 0.5
	deathsound "weapons/zookahit"
	states
	{
		Spawn:
			TORP A 0 bright
			TORP A 2 bright A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			wait
		Death:
			TORE A 0 bright A_NoGravity
			TORE A 0 bright A_SetScale(1.0,1.0)
			TORE A 0 bright A_SetTranslucent(1.0,1)
			TORE A 4 bright A_Explode(128,192)
			TORE BBCC 2 bright A_SpawnItemEx("HacxPhotonExplode",frandom(-24,24),frandom(-24,24),frandom(-8,8))
			TORE DE 4 bright
			stop
	}
}
Actor HacxPhotonExplode : HacxTrail
{
	Alpha 1
	Renderstyle Add
	Scale 0.75
	States
	{
		Spawn:
			TORE ABCDE 3 bright
			stop
	}	
}

actor HacxPhotonGravity : HacxPhoton
{
	-NOGRAVITY
	gravity 0.25
}

actor HacxEnemyPhoton : HacxPhoton
{
	seesound "weapons/zookafire"
	damage 0	
	states
	{
		Death:
			TORE A 0 bright A_NoGravity
			TORE A 0 bright A_SetScale(1.0,1.0)
			TORE A 0 bright A_SetTranslucent(1.0,1)
			TORE A 4 bright A_Explode(80,192)
			TORE BCDE 4 bright
			stop
	}
}

/* old actor -- accelerating projectile. A proper "torpedo", but irritating to use.
actor HacxPhoton : Rocket
{
	//game Hacx
	spawnid 127
	obituary "$OB_MPZOOKA"
	speed 5
	scale 0.5
	seesound "weapons/zookafire"
	deathsound "weapons/zookahit"
	states
	{
		Spawn:
			TORP A 0 bright
			TORP A 2 bright A_ScaleVelocity(1.25)
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright A_ScaleVelocity(1.25)
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright A_ScaleVelocity(1.25)
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright A_ScaleVelocity(1.25)
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright A_ScaleVelocity(1.25)
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright A_ScaleVelocity(1.25)
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright A_ScaleVelocity(1.25)
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright A_ScaleVelocity(1.25)
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright A_ScaleVelocity(1.25)
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright A_ScaleVelocity(1.25)
		SLoop:
			TORP A 0 A_SpawnItemEx("HacxPhotonTrail",0,0.2*random(-10,10),0.2*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			TORP A 2 bright
			goto SLoop
		Death:
			TNT1 A 1 A_SpawnItemEx("HacxPhotonExplode",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS,0)
			stop
	}
}
*/
/*
Actor HacxPhotonExplode : HacxPuff
{
	Alpha 1
	Renderstyle Add
	VSpeed 0
	States
	{
		Spawn:
			TORE A 4 bright
			TORE B 4 bright
			TORE C 4 bright
			TORE D 4 bright
			TORE E 4 bright
			stop
	}	
}
*/
Actor HacxPhotonTrail : HacxTrail
{
	Alpha 0.75
	Scale 0.15
	Renderstyle Add
	VSpeed 1
	States
	{
	Spawn:
		TPHO ABCDEBA 2 bright A_FadeOut(0.1)
		Stop
	}	
}

// Anti-Gun -----------------------------------------------------------------

actor HacxAntigun : HacxWeapon replaces PlasmaRifle
{
	//game Hacx
	SpawnID 30
	Weapon.SelectionOrder 100
	Weapon.UpSound "weapons/antigunup"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "HacxMolecules"
	Inventory.PickupMessage "$GOTANTIGUN"
	Tag "$TAG_ANTIGUN"
	states
	{
		Spawn:
			ANTI A -1
			Stop
		Ready:
			ANTG A 1 A_WeaponReady
			Loop
		Idle:
			ANTG A 0
			Goto IdleWait
		Deselect:
			ANTG A 0 A_Lower
			ANTG A 1 A_Lower
			Loop
		Select:
			ANTG A 0 A_Raise
			ANTG A 1 A_Raise
			Loop
		Fire:
			ANTG A 0 A_GunFlash
			ANTF A 2 A_FireCustomMissile ("HacxMolecule")
			ANTF B 2 A_FireCustomMissile ("HacxMolecule")
			ANTG B 20 A_ReFire
			goto Ready
		Flash:
			TNT1 A 2 bright A_Light1
			TNT1 B 3 bright A_Light2
			TNT1 B 0 A_Light0
			stop
		AltFire:
			ANTK A 0
			Goto Kick
	}
}

actor HacxMolecule : FastProjectile
{
	//game Hacx
	spawnid 51
	Radius 13
	Height 8
	Speed 100
	Damage 4
	Projectile
	+RANDOMIZE
	RenderStyle Translucent
	Alpha 0.8
	obituary "$OB_MPANTIGUN"
	seesound "weapons/antigun"
	deathsound "weapons/antigunhit"
	MissileType "HacxMoleculeTrail"
	MissileHeight 8
	states
	{
		Spawn:
			MOLS AB 2 bright
			loop
		Death:
			MOLE ABCD 3 bright
			Stop		
	}
}

Actor HacxMoleculeTrail : HacxTrail
{
	RenderStyle Translucent
	Alpha 0.8
	States
	{
		Spawn:
			MOLS AB 1 bright A_FadeOut(0.33,1)
			loop
	}	
}

// Nuker --------------------------------------------------------------------

actor HacxNuker : HacxWeapon replaces BFG9000
{
	//game Hacx
	SpawnID 31
	Height 20
	Weapon.SelectionOrder 2800
	Weapon.UpSound "weapons/nukerup"
	Weapon.AmmoUse 50
	Weapon.AmmoGive 50
	weapon.ammotype "HacxMolecules"
	Inventory.PickupMessage "$GOTNUKER"
	Tag "$TAG_NUKER"
	+WEAPON.BFG
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	states
	{
		Spawn:
			NUKR A -1
			Stop
		Ready:
			NUKG A 1 A_WeaponReady
			Loop
		Idle:
			NUKG A 0
			Goto IdleWait
		Deselect:
			NUKG A 0 A_Lower
			NUKG A 1 A_Lower
			Loop
		Select:
			NUKG A 0 A_Raise
			NUKG A 1 A_Raise
			Loop
		Fire:
			NUKG A 15 A_PlayWeaponSound ("weapons/nuker")
			NUKG B 3 A_GunFlash
			NUKG B 7 A_FireCustomMissile ("HacxNukeShot",0,1,0,-8,0,10)
			NUKG A 15
			NUKG A 0 A_Refire
			goto Ready
		Flash:
			NUKF A 3 bright A_Light1
			NUKF B 4 bright A_Light2
			NUKF B 0 bright A_Light0
			stop
		AltFire:
			NUKK A 0
			Goto Kick
	}
}

Actor HacxNukeShot : HacxProjectile
{
	//game Hacx
	Radius 8
	Damage 10
	spawnid 128
	Speed 30
	Scale 0.5
	Obituary "$OB_MPNUKE"
	Renderstyle Normal
	Bouncetype Hexen
	Bouncefactor 0.5
	Wallbouncefactor 0.25
	-NOGRAVITY
	+BOUNCEONACTORS
	States
	{
	Spawn:
		NCAN ABCDABCDABCDABCD 2 bright
		NCAN A 0 A_Stop
		NCAN A 0 A_Die
		// Fallthrough, just in case.
	Death:
		NUK1 A 0 bright A_NoGravity
		NUK1 A 0 bright A_Explode(128,256,1)
		NUK1 A 0 bright A_PlaySound("weapons/nukehit", 0, 1.0, 0, 0.5)
		NUK1 A 0 bright A_SetScale(1.0, 1.0)
		NUK1 A 5 bright A_SetTranslucent(1.0, 1) //A_SpawnItemEx("HacxNukeBoom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		NUK1 B 0 bright A_Mushroom("HacxNukeSpray", 50, MSF_Classic, 0.5, 1.0)
		NUK1 B 5 bright A_Mushroom("HacxNukeSmoke", 20, MSF_Classic, 2.0, 0.2)
		NUK1 CDEF 5 bright 
		Stop
	}
}
/*
actor HacxNukeBoom
{
	//game Hacx
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 1
	States
	{
	Spawn:
		NUK1 ABCDEF 5 Bright
		Stop
	}
}
*/
actor HacxNukeSmoke
{
	//game Hacx
	Projectile
	+THRUACTORS
	Radius 13
	Speed 15
	RenderStyle Add
	Alpha 0.5
	Gravity 0.5
	States
	{
	Spawn:
		NUK1 A 0 BRIGHT
		NUK1 A 0 bright A_ScaleVelocity(1.0+0.05*random(-5,5))
		NUK1 A 0 BRIGHT A_Jump(128,2)
		NUK2 D 0 BRIGHT
		"----" AAAA 4 bright
	Death:
		"----" A 0 A_ScaleVelocity(1.5)
		"----" A 4 bright A_FadeOut(0.125)
		Goto Death
	}
}

actor HacxNukeSpray : HacxProjectile
{
	//game Hacx
	Speed 15
	Damage 5
	spawnid 51
	obituary "$OB_MPNUKE"
	+BOUNCEONWALLS
	Wallbouncefactor 0.5
	Damagetype Extreme
	RenderStyle Add
	Alpha 0.75
	states
	{
		Spawn:
			NBAL A 0 bright
			NBAL A 0 bright A_ScaleVelocity(1.0+0.05*random(-5,5))
			NBAL AB 2 bright
			loop
	    Death:
		    NUK2 B 4 bright
			NUK2 A 4 bright A_Explode(20, 64, 1, 1, 64)
			NUK2 BCD 4 bright
			stop
	}
}


				      /* ---------- ENEMIES ---------- */


// Thug ---------------------------------------------------------------------

actor HacxThug replaces Zombieman
{
	//game Hacx
	spawnid 4
	obituary "$OB_THUG"
	health 50
	radius 21
	height 72
	mass 45
	speed 12
	painchance 100
	seesound "thug/sight"
	attacksound "thug/attack"
	painsound "thug/pain"
	deathsound "thug/death"
	activesound "thug/active"
	dropitem "HacxRounds" 256
	MONSTER
	+FLOORCLIP
	states
	{
		Spawn:
			THUG A 10 A_Look
			loop
		See:
			THUG AABBCCDD 3 A_Chase
			loop
		Missile:
			THUG E 6 A_FaceTarget
			THUG F 3 bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "HacxPuff", 0, CBAF_NORANDOM)
			THUG E 6
			goto See
		Pain:
			THUG G 4
			THUG G 4 A_Pain
			goto See
		Death:
			THUG H 9
			THUG I 5 A_Scream
			THUG J 4 A_NoBlocking
			THUG K 4
			THUG L -1
			stop
		XDeath:
			THUG M 3
			THUG N 3 A_XScream
			THUG O 3 A_NoBlocking
			THUG PQRST 3
			THUG U -1
			stop
		Raise:
			THUG KJIH 5
			goto See
	}
}

// Android ------------------------------------------------------------------

actor HacxAndroid replaces ShotgunGuy
{
	//game Hacx
	spawnid 1
	obituary "$OB_ANDROID"
	health 75
	radius 21
	height 70
	mass 55
	speed 16
	painchance 20
	painchance "Reznator", 255
	damagefactor "Reznator", 1.5
	seesound "android/sight"
	attacksound "android/attack"
	painsound "android/pain"
	deathsound "android/death"
	activesound "android/active"
	dropitem "HacxTazer" 256
	scale 0.9
	MONSTER
	+FLOORCLIP
	+NOBLOOD
	states
	{
		Spawn:
			ANDY E 10 A_Look
			loop
		See:
			ANDY AABBCCDD 3 A_Chase
			loop
		Missile:
			ANDY E 10 A_FaceTarget
			ANDY E 0 A_PlaySound("android/attack")
			ANDY FF 0 A_CustomMissile("HacxAndroidShot",32,0,frandom(-22.5, 22.5))
			ANDY F 10 bright  A_CustomMissile("HacxAndroidShot",32,0,0)
			ANDY E 10 //A_CustomBulletAttack (22.5, 0, 3, random(1,5) * 3, "TazerPuff", 0, CBAF_NORANDOM)
			goto See
		Pain:
			ANDY G 3
			ANDY G 3 A_Pain
			goto See
		Death:
			ANDY H 9 bright
			ANDY I 6 bright A_Scream
			ANDY J 4 bright A_NoBlocking
			ANDY K 4
			ANDY L -1
			stop
		XDeath:
			ANDY H 0 bright A_Scream
			ANDY H 10 bright A_PlaySound("android/gib",CHAN_AUTO)
			ANDY M 2 bright 
			ANDY N 4 bright A_PlaySound("misc/explode",CHAN_VOICE)
			ANDY O 0 bright A_Explode(128,128,0)
			ANDY O 4 bright A_NoBlocking
			ANDY PQR 4 bright
			ANDY ST 4
			ANDY U -1
			stop
		Raise:
			ANDY LKJIH 5
			goto See
	}
}

actor HacxAndroidShot : TazerShot
{
	//game Hacx
	speed 40
	damage (5*random(1,3)) // same as standard Doom hitscan
	seesound ""
	deathsound "weapons/tazerhit"
	decal "TazerScorch"
	states
	{
		Spawn:
			TPUF AABBBAA 1 bright A_SpawnItemEx("TazerTrail2",0,0.5*random(-10,10),0.5*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
			loop
		Death:
			TPUF CDE 2 bright
			stop
	}
}

// Phage --------------------------------------------------------------------

actor HacxPhage replaces Fatso
{
	//game Hacx
	spawnid 112
	obituary "$OB_PHAGE"
	health 150
	speed 22
	radius 25
	height 96
	damage 5
	meleerange 128
	painchance 75
	mass 150
	seesound "phage/sight"
	painsound "phage/pain"
	deathsound "phage/death"
	activesound "phage/active"
	MONSTER
	+FLOORCLIP
	states
	{
		Spawn:
			PHAG G 10 A_Look
			loop
		See:
		Chase:
			PHAG AABB 4 A_Chase
			loop
		Melee:
			PHAG D 10 A_FaceTarget
			PHAG E 5 bright A_PlaySound("phage/melee")
			PHAG F 10 bright A_SkullAttack
			PHAG D 10 A_Stop
			goto Chase
		Missile:
			PHAG G 0 A_PlaySound("phage/attack")
			PHAG H 10 A_FaceTarget
			PHAG I 10 bright A_CustomMissile ("HacxPhageBall", 32.0, 0.0, 0)
			PHAG G 5 bright A_FaceTarget
			PHAG H 5 A_FaceTarget
			PHAG I 10 bright A_CustomMissile ("HacxPhageBall", 32.0, 0.0, 0)
			PHAG G 5 bright A_FaceTarget
			PHAG H 5 A_FaceTarget
			PHAG I 10 bright A_CustomMissile ("HacxPhageBall", 32.0, 0.0, 0)
			PHAG G 5 bright A_FaceTarget
			goto Chase
		Pain:
			PHAG J 3
			PHAG J 3 A_Pain
			goto Chase
		Death:
			PHAG K 5
			PHAG L 5 A_Scream
			PHAG M 5 A_Fall
			PHAG NOPQR 5
		DeathLoop:
			PHAG S 18
			PHAG T 6 
			goto DeathLoop
	}
}

actor HacxPhageBall : ArachnotronPlasma
{
	//game Hacx
	spawnid 129
	speed 16
	height 4
	damage 3
	mass 100
	seesound "misc/projectilefire"
	deathsound "misc/projectilehit"
	PROJECTILE
	+RANDOMIZE
	states
	{
		Spawn:
			PHGB AB 4 BRIGHT
			loop
		Death:
			PHGX ABCDE 5 bright
			stop
	}
}

// Buzzer -------------------------------------------------------------------

actor HacxBuzzer replaces Demon
{
	//game Hacx
	spawnid 8
	hitobituary "$OB_BUZZER"
	health 150
	speed 12
	radius 25
	height 68
	painchance 100
	painchance "Reznator", 255
	damagefactor "Reznator", 1.5
	mass 600
	seesound "buzzer/sight"
	attacksound "buzzer/attack"
	painsound "buzzer/pain"
	deathsound "buzzer/death"
	activesound "buzzer/active"
	MONSTER
	+FLOORCLIP
	+NOGRAVITY
	+FLOAT
	+NOBLOOD
	states
	{
		Spawn:
			BUZZ A 1 A_Look
			loop
		See:
			BUZZ AABBCCDD 2 A_Chase
			loop
		Melee:
			BUZZ E 4 A_FaceTarget
			BUZZ F 4 Bright A_FaceTarget
			BUZZ G 5 Bright A_SargAttack
			BUZZ F 5 Bright 
			goto See
		Pain:
			BUZZ H 2 bright
			BUZZ H 8 bright A_Pain
			goto See
		Death:
			BUZZ I 5 bright A_SetTranslucent(1.0,0)
			BUZZ J 5 bright A_Scream
			BUZZ K 5 bright A_CustomMissile("HacxBuzzerDamage", 32)
			BUZZ L 5 bright
			BUZZ M 5 bright
			BUZZ N -1 A_NoBlocking
			stop
		Death.Reznator:
			BUZZ I 0 bright A_SetTranslucent(1.0,0)
			BUZZ I 5 bright A_PlaySound("buzzer/disable")
			BUZZ N -1 A_NoBlocking
			stop
		Raise:
			BUZZ NMLKJI 5
			goto See
	}
}

actor HacxBuzzerDamage : HacxProjectile
{
	//game Hacx
	+THRUACTORS
	Damage 0
	Speed 0
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 BRIGHT
	Death:
		TNT1 A 1 bright A_Explode(96,128,0)
		Stop
	}
}

// Stealth ------------------------------------------------------------------

actor HacxStealth : HacxBuzzer replaces Spectre
{
	//game Hacx
	spawnid 9
	hitobituary "$OB_STEALTH"
	renderstyle Stencil
	stencilcolor "00 00 00"
	alpha 0.2
	speed 8
	health 30
	painchance 250
	seesound ""
	painsound ""
	activesound ""
}

// ICE ----------------------------------------------------------------------

actor HacxICE replaces DoomImp
{
	//game Hacx
	spawnid 5
	obituary "$OB_ICE"
	health 200
	speed 15
	radius 32
	height 55
	damage 6
	painchance 10
	seesound "ice/sight"
	attacksound "ice/attack"
	painsound "ice/pain"
	deathsound "ice/death"
	activesound "ice/active"
	MONSTER
	+FLOORCLIP
	states
	{
		Spawn:
			ICES A 10 A_Look
			loop
		See:
			ICES AABBCCDD 4 A_Chase
			loop
		Missile:
			ICES E 6 A_FaceTarget
			ICES F 20 A_SkullAttack
			ICES G 6 
			ICES A 6 A_Stop 
			loop
		Pain:
			ICES H 10 A_Pain
			ICES H 10
			goto See
		Death:
		XDeath:
			ICES N 10
			ICES O 5 A_Scream
			ICES P 5
			ICES Q 5 A_NoBlocking
			ICES RS 5
			ICES T -1
			stop
		Raise:
			ICES SRQPON 5
			goto See
	}
}

// GENIE Spawn -----------------------------------------------------------------

actor HacxGenieSpawn replaces LostSoul
{
	//game Hacx
	spawnid 110
	obituary "$OB_GENIESPAWN"
	health 250
	speed 10
	radius 48
	height 77
	damage 9
	painchance 31
	mass 600
	attacksound "spawn/attack"
	painsound "spawn/pain"
	deathsound "spawn/death"
	activesound "spawn/active"
	MONSTER
	+FLOORCLIP
	+NOGRAVITY
	+FLOAT
	+NOBLOOD
	damagetype "Ramming"
	states
	{
		Spawn:
			GSPN A 10 A_Look
			loop
		See:
			GSPN A 2 A_Chase
			GSPN A 2 A_Chase("","")
			loop
		Missile:
			GSPN B 10 bright A_FaceTarget
			GSPN C 0 bright A_FaceTarget
			GSPN C 0 bright A_SkullAttack(30)
			GSPN CCCCCCCCCC 2 bright A_SpawnItemEx("HacxGenieSpawnTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
			GSPN DDDDD 2 bright A_ScaleVelocity(0.25)
			goto Missile+1
		Pain:
			GSPN D 3
			GSPN D 7 A_Pain
			goto Missile+1
		Death:
			GSPN K 4
			GSPN L 4 A_Scream
			GSPN M 4
			GSPN N 4 A_NoBlocking
			GSPN OPQ 4
			GSPN R -1
			stop
	}
}
actor HacxGenieSpawnTrail
{
	+NOINTERACTION
	Alpha 0.6
	States
	{
	Spawn:
		GSPN CCCCCCC 1 bright A_FadeOut(0.075)
		Stop
	}
}

// Majong 7 -----------------------------------------------------------------

actor HacxMajong7 replaces PainElemental
{
	//game Hacx
	spawnid 115
	health 400
	speed 12
	radius 31
	height 56
	painchance 0
	mass 400
	seesound "majong7/sight"
	painsound "majong7/pain"
	deathsound "majong7/death"
	activesound "majong7/active"
	MONSTER
	+FLOORCLIP
	+NOBLOOD
	+NORADIUSDMG
	states
	{
		Spawn:
			MAJ7 A 10 A_Look
			loop
		See:
			MAJ7 A 0 A_PlaySound("majong7/walk")
			MAJ7 AABB 4 A_Chase
			MAJ7 A 0 A_PlaySound("majong7/walk")
			MAJ7 DDCC 4 A_Chase
			loop
		Missile:
			VILE LL 5 bright A_FaceTarget
			MAJ7 E 5 bright A_FaceTarget
			MAJ7 F 0 bright A_PlaySound("weapons/zookafire")
			MAJ7 F 5 bright A_CustomMissile("HacxEnemyPhoton",48,0,0)
			/*
			MAJ7 F 0 bright A_JumpIfCloser(512 ,"M1")
			MAJ7 F 0 bright A_JumpIfCloser(1024,"M2")
			MAJ7 F 0 bright A_JumpIfCloser(1536,"M3")
			MAJ7 F 0 bright A_JumpIfCloser(2048,"M4")
			MAJ7 F 5 bright A_CustomMissile("HacxEnemyPhoton",48,0,0,CMF_AIMDIRECTION,15)
			goto See
		   M4:
			MAJ7 F 5 bright A_CustomMissile("HacxEnemyPhoton",48,0,0,CMF_AIMDIRECTION,10)
			goto See
		   M3:
			MAJ7 F 5 bright A_CustomMissile("HacxEnemyPhoton",48,0,0,CMF_AIMDIRECTION,5)
			goto See
		   M2:
			MAJ7 F 5 bright A_CustomMissile("HacxEnemyPhoton",48,0,0,CMF_AIMDIRECTION,2)
			goto See
		   M1:
			MAJ7 F 5 bright A_CustomMissile("HacxEnemyPhoton",48,0,0,CMF_AIMDIRECTION,0)
			*/
			goto See
		Pain:
			MAJ7 A 1
			goto See
		Death:
			MAJ7 G 7
			MAJ7 H 7 A_Scream
			MAJ7 I 7 A_NoBlocking
			MAJ7 JK 7
			MAJ7 L -1
			stop
		Raise:
			MAJ7 LKJIHG 8
			goto See
	}
}

// Monstruct ----------------------------------------------------------------

actor HacxMonstruct replaces ChaingunGuy
{
	//game Hacx
	spawnid 2
	obituary "$OB_MONSTRUCT"
	health 400
	speed 7
	radius 35
	height 87
	painchance 50
	mass 4000
	seesound "monstruct/sight"
	painsound "monstruct/pain"
	deathsound "monstruct/death"
	activesound "monstruct/active"
	attacksound "weapons/pistol"
	damagefactor "Monstruct", 0.0
	meleerange 128
	MONSTER
	+FLOORCLIP
	states
	{
		Spawn:
			MONS A 10 A_Look
			loop
		See:
			MONS AABBCCDD 4 A_Chase
			loop
		Missile:
			MONS G 0 A_JumpIfCloser(384,"Melee")
			MONS G 10 bright A_FaceTarget // E: right, F: left
			MONS E 6 bright A_CustomMissile("HacxMonstructShot", 32, 24, 0)
			MONS F 6 bright A_CustomMissile("HacxMonstructShot", 32, -24, frandom(0,22.5))
			MONS G 6 bright A_FaceTarget
			MONS E 6 bright A_CustomMissile("HacxMonstructShot", 32, 24, frandom(-22.5, 0))
			MONS F 6 bright A_CustomMissile("HacxMonstructShot", 32, -24, 0)
			MONS G 6
			Goto See
		Melee:
			MONS G 6 A_FaceTarget
		   MeleeLoop:
			MONS O 2 bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "HacxMonstructPuff", 0, CBAF_NORANDOM)
			MONS N 3
			MONS O 0 A_JumpIfCloser (512, 1)
			Goto See
			MONS O 0 A_CPosRefire
			goto MeleeLoop
		Pain:
			MONS D 5 A_Chase
			goto See
		Death:
			MONS H 5
			MONS H 5 A_Scream
			MONS I 10
			MONS J 10 A_NoBlocking
			MONS K 8
			MONS L 6
			MONS M -1
			stop
		Raise:
			MONS MLKJIH 5
			goto See
	}
}

actor HacxMonstructShot
{
	//game Hacx
	spawnid 153
	speed 20
	radius 6
	height 8
	damage 8
	mass 100
	seesound "misc/projectilefire"
	deathsound "misc/explode"
	damagetype Monstruct
	PROJECTILE
	+RANDOMIZE
	states
	{
		Spawn:
			MONF AB 4 bright
			loop
		Death:
			TORE ABC 3 bright
			TORE D 5 bright A_Explode(32,128,0)
			TORE E 5 bright
			stop
	}
}

actor HacxMonstructPuff : HacxPuff
{
	damagetype Monstruct
}

// Terminatrix --------------------------------------------------------------

actor HacxTerminatrix replaces BaronOfHell
{
	//game Hacx
	spawnid 3
	obituary "$OB_TERMIN"
	hitobituary "$OB_TERMINHIT"
	health 800
	speed 12
	radius 24
	height 96
	painchance 40
	mass 1000
	seesound "terminatrix/sight"
	painsound "terminatrix/pain"
	deathsound "terminatrix/death"
	activesound "terminatrix/active"
	MONSTER
	+FLOORCLIP
	states
	{
		Spawn:
			TRIX AB 10 A_Look
			loop
		See:
			TRIX A 3 A_Chase
			TRIX A 3 A_VileChase
			TRIX B 3 A_Chase
			TRIX B 3 A_VileChase
			TRIX C 3 A_Chase
			TRIX C 3 A_VileChase
			TRIX D 3 A_Chase
			TRIX D 3 A_VileChase
			loop
		Melee:
			TRIX P 8 A_FaceTarget
			TRIX Q 8 A_VileAttack("misc/explode")
			TRIX R 20
			goto See
		Missile:
			TRIX EF 8 A_FaceTarget
			TRIX G 8 A_CustomMissile ("HacxTerminatrixBall", 32.0, 0, 0)
			goto See
		Heal:
			TRIX STU 8 BRIGHT
			Goto See
		Pain:
			TRIX H 7 bright A_Pain
			TRIX H 7 bright
			goto See
		Death:
			TRIX I 8
			TRIX J 8 A_Scream
			TRIX K 8
			TRIX L 8 A_NoBlocking
			TRIX MN 8
			TRIX O -1 A_BossDeath
			stop
	}
}

actor HacxTerminatrixBall
{
	//game Hacx
	spawnid 154
	health 1000
	speed 20
	radius 6
	height 8
	damage 10
	mass 100
	seesound "misc/projectilefire"
	deathsound "misc/projectilehit"
	PROJECTILE
	+RANDOMIZE
	states
	{
		Spawn:
			TRXB AAAABBBB 1 bright A_SpawnItemEx("HacxTerminatrixTrail",0,0,0,0,0,0,0,128,0)
			loop
		Death:
			PHGX ABCDE 5 bright
			stop
	}
}

Actor HacxTerminatrixTrail : HacxPuff
{
	Alpha 1
	Renderstyle Add
	VSpeed 0
	States
	{
		Spawn:
			TRXB AAAABBBB 1 bright A_FadeOut(0.1,1)
			loop
	}	
}

actor HacxOverlordDummy
{
	//game Hacx
	radius 20
	height 40
	states
	{
		Spawn:
			OVLD A -1
			stop
	}
}

// Mechamaniac --------------------------------------------------------------

actor HacxMechaManiac replaces HellKnight
{
	//game Hacx
	spawnid 113
	health 600
	speed 8
	radius 24
	height 96
	painchance 40
	//scale 1.15
	mass 1000
	seesound "mecha/sight"
	painsound "mecha/pain"
	deathsound "mecha/death"
	obituary "$OB_MANIAC"
	MONSTER
	+FLOORCLIP
	states
	{
		Spawn:
			MECH E 10 A_Look
			loop
		See:
			MECH AAAABBBB 2 A_Chase
			MECH B 0 A_PlaySound("mecha/walk", CHAN_BODY)
			MECH CCCCDDDD 2 A_Chase
			MECH D 0 A_PlaySound("mecha/walk", CHAN_BODY)
			loop
		Melee:
		Missile:
			MECH E 8 bright A_FaceTarget
			MECH F 2 bright A_CustomMissile ("HacxMechaShot", 48.0, -24.0, 0)
			MECH GE 2 bright A_FaceTarget
			MECH H 2 bright A_CustomMissile ("HacxMechaShot", 48.0,  24.0, 0)
			MECH IE 2 bright A_FaceTarget
			MECH F 2 bright A_CustomMissile ("HacxMechaShot", 48.0, -24.0, 0)
			MECH GEE 2 bright A_FaceTarget
			MECH D 0 A_PlaySound("mecha/walk", CHAN_BODY)
			goto See
		Pain:
			MECH J 3
			MECH J 3 A_Pain
			goto See
		Death:
			MECH K 6 bright A_Scream
			MECH L 6 bright A_PlaySound("misc/explode2") 
			MECH M 6 bright
			MECH N 6 bright A_NoBlocking
			MECH OP 6 bright
			MECH Q -1
			stop
		Raise:
			MECH PONMLK 6
			goto See
	}
}

actor HacxMechaShot : HacxMolecule
{
	speed 50
	damage 3
}

// Thorn Thing --------------------------------------------------------------

actor HacxThornThing replaces Arachnotron
{
	//game Hacx
	spawnid 6
	obituary "$OB_THORN"
	health 600
	speed 20
	radius 64
	height 96
	painchance 20
	mass 600
	seesound "thorn/sight"
	painsound "thorn/sight"
	deathsound "thorn/death"
	activesound "thorn/active"
	scale 1.15
	MONSTER
	+FLOORCLIP
	states
	{
		Spawn:
			THRN AB 10 A_Look
			loop
		See:
			THRN A 10
		Walk:
			THRN AA 3 A_Chase
			THRN A 0 A_PlaySound("thorn/walk")
			THRN BBCCDD 3 A_Chase
			Loop
		Missile:
			THRN E 8 bright A_FaceTarget
			THRN E 0 bright A_CustomMissile ("HacxThornThingBall", 64.0,  32, 0)
			THRN F 6 bright A_CustomMissile ("HacxThornThingBall", 64.0, -32, 0)
			THRN E 6 bright A_FaceTarget
			goto Walk
		Pain:
			THRN G 3
			THRN G 3 A_Pain
			goto Walk
		Death:
			THRN H 7 A_Scream
			THRN I 7 A_NoBlocking
			THRN JKLMN 7
			THRN O -1
			stop
		Raise:
			THRN MNLKJIH 5
			goto See
	}
}

actor HacxThornThingStationary : HacxThornThing replaces DeadLostSoul
{
	speed 0
	states
	{
		See:
			THRN A 3 A_Chase
			loop
		Missile:
			THRN A 8 bright A_FaceTarget
			THRN D 0 bright A_CustomMissile ("HacxThornThingBall", 40.0,  32, 0)
			THRN D 3 bright A_CustomMissile ("HacxThornThingBall", 40.0, -32, 0)
			THRN E 3 bright
			THRN D 6 bright A_FaceTarget
			goto See
		Pain:
			THRN F 3
			THRN F 3 A_Pain
			goto See
		Raise:
			THRN LKJIHG 5
			goto See
	}
}

actor HacxThornThingBall : RevenantTracer
{
	//game Hacx
	spawnid 53
	mass 100
	damage 5
	seesound "thorn/attack"
	deathsound "misc/projectilehit"
	states
	{
		Spawn:
			THNB ABABABABABABABABABAB 1 bright A_Tracer
			THNB A 0 bright A_PlaySound("thorn/fly", CHAN_VOICE, 1.0, 1)
		Fly:		
			THNB AB 1 bright A_Tracer
			goto Fly
		Death:
			THNB C 8 bright
			THNB D 6 bright
			THNB E 4 bright
			stop
	}
}

// Replicant --------------------------------------------------------------

actor HacxReplicant replaces Revenant
{
	//game Hacx
	spawnid 20
	obituary "$OB_REPLICANT"
	health 150
	radius 21
	height 72
	scale 0.9
	mass 45
	speed 16
	painchance 100
	seesound "replicant/sight"
	attacksound "weapons/pistol"
	painsound "replicant/pain"
	deathsound "replicant/death"
	activesound "replicant/active"
	dropitem "HacxUzi" 256
	MONSTER
	+FLOORCLIP
	states
	{
	Spawn:
		REPL A 10 A_Look
		loop
	See:
		REPL BBCCDDEE 2 A_Chase
		loop
	Missile:
		REPL F 0 A_PlaySound("replicant/attack")
	MissileLoop:
		REPL F 10 A_FaceTarget
		REPL G 2 bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 2, "HacxPuff", 0, CBAF_NORANDOM)
		REPL F 1 A_FaceTarget
		REPL G 2 bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 2, "HacxPuff", 0, CBAF_NORANDOM)
		REPL F 1 A_FaceTarget 
		REPL G 2 bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 2, "HacxPuff", 0, CBAF_NORANDOM)
		REPL F 1 A_FaceTarget 
		REPL G 2 bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 2, "HacxPuff", 0, CBAF_NORANDOM)
		REPL F 1 A_FaceTarget 
		REPL G 2 bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 2, "HacxPuff", 0, CBAF_NORANDOM)
		REPL F 0 A_CPosRefire
		Loop
	Pain:
		REPL H 2
		REPL H 2 A_Pain
		goto See
	Death:
		REPL I 6
		REPL J 5 A_PlayerScream
		REPL K 4 A_NoBlocking
		REPL L 4
		REPL MN 4
		REPL O -1
		Stop
	Raise:
		REPL NMLKJIH 4
		goto See
	}
}

// GENIE --------------------------------------------------------------

actor HacxGenie replaces Cyberdemon
{
	//game Hacx
	spawnid 114
	health 8000
	speed 16
	radius 40
	height 110
	painchance 10
	mass 0x7FFFFFF
	seesound "genie/sight"
	activesound "genie/active"
	painsound "genie/pain"
	deathsound "genie/death"
	obituary "$OB_GENIE"
	meleerange 192
	MONSTER
	+BOSS
	+FLOORCLIP
	+FULLVOLACTIVE
	states
	{
		Spawn:
			GENI A 0 bright A_PlaySound("genie/engine", CHAN_BODY, 1.0, 1)
			GENI ACBC 4 bright A_Look
			loop
		See:
			GENI A 0 bright A_PlaySound("genie/engine", CHAN_BODY, 1.0, 1)
			GENI AABBCC 2 bright A_Chase
			loop
		Melee:
			GENI D 0 bright A_PlaySound("genie/claw", CHAN_VOICE)
			GENI D 2 bright A_FaceTarget
			GENI D 1 bright A_Recoil(-8.0)
			GENI D 1 bright A_FaceTarget
			GENI D 1 bright A_Recoil(-8.0)
			GENI D 1 bright A_FaceTarget
			GENI D 1 bright A_Recoil(-8.0)
			GENI D 1 bright A_FaceTarget
			GENI E 4 bright A_FaceTarget
			GENI F 2 bright A_CustomMeleeAttack(10,"genie/slash","")
			GENI F 8 bright 
			goto See
		Missile:
			GENI AH 3 bright A_FaceTarget
			GENI H 0 bright A_Jump(128,"Laser")
			GENI H 5 bright A_CustomMissile("HacxEnemyPhoton", 64.0, 0, 0)
			GENI H 3 bright A_FaceTarget
			goto See
		Laser:
			GENI HHHHHHHH 2 bright A_CustomMissile("HacxMechaShot", 64.0, 0, 0)
			GENI H 3 bright A_FaceTarget
			goto See			
		Pain:
			GENI I 2 bright 
			GENI I 8 bright A_Pain
			goto See
		Death:
			GENI J 6 bright A_Scream
			GENI K 6 bright A_StopSound(CHAN_BODY)
			GENI L 6 bright
			GENI M 6 bright A_NoBlocking
			GENI NOP 6 bright
			NUK1 B 5 bright A_PlaySound("misc/explode")
			NUK1 CDEF 5 bright
			TNT1 A -1 bright
			stop
	}
}

// Roaming Mine -------------------------------------------------------------

actor HacxRoamingMine replaces WolfensteinSS
{
	//game Hacx
	spawnid 16
	obituary "$OB_MINE"
	health 80
	speed 12
	radius 16
	height 40
	painchance 0
	mass 600
	deathsound "misc/explode"
	activesound "mine/active"
	MONSTER
	+FLOORCLIP
	+NOBLOOD
	+FLOAT
	+NOGRAVITY
	states
	{
		Spawn:
			MINE I 2 A_Look
			loop
		See:
			MINE JKLMNOABCDEFGHI 2 A_Chase
			loop
		Melee:
			MINE A 0 A_Die
		Death:
			MEXP A 4 bright A_NoBlocking
			MEXP B 0 bright A_SetTranslucent(1.0,1)
			MEXP B 0 bright A_Scream
			MEXP B 4 bright A_Explode
			MEXP C 4 bright
			MEXP DEF 3 bright
			stop
	}
}

// Glass --------------------------------------------------------------------

actor HacxGlass replaces Cacodemon
{
	//game Hacx
	spawnid 19
	obituary "$OB_GLASS"
	health 1
	speed 8
	radius 36
	height 128
	painchance 256
	mass 6000
	deathsound "glass/break"
	+ISMONSTER
	+SHOOTABLE
	+NOBLOOD
	+FLOORCLIP
	states
	{
		Spawn:
			TNT1 A -1 A_Look
			stop
		Death:
			GLAS A 2
			GLAS B 2 A_Scream
			GLAS C 2 A_NoBlocking
			GLAS D 2 A_FaceTarget
			GLAS E 4 A_CustomMissile ("HacxGlassShards", 32.0, 0, 180)
			GLAS FGHIJ 4
			stop
	}
}

actor HacxGlassShards
{
	//game Hacx
	spawnid 126
	obituary "$OB_GLASS"
	speed 10
	radius 6
	height 8
	damage 2
	PROJECTILE
	+DROPOFF
	-NOGRAVITY
	states
	{
		Spawn:
			SHRD AB 2
			loop
		Death:
			SHRD CD 7
			SHRD E -1
			stop
	}
}

actor HacxGlassAuto : HacxGlass 3000
{
	-SHOOTABLE
	states
	{
		Spawn:
			TNT1 A 4 A_Look
			Loop
		See:
			TNT1 A 1 A_Die
			goto Death
	}
}
actor HacxGlassSolid : HacxGlass 2999 { +SOLID }


				       /* ---------- ITEMS ---------- */


// Rounds ------------------------------------------------------------------

actor HacxRounds : Ammo replaces Clip
{
	//game Hacx
	spawnid 11
	inventory.pickupmessage "$GOTROUNDS"
	inventory.amount 20
	inventory.maxamount 200
	ammo.backpackamount 20
	ammo.backpackmaxamount 400
	inventory.icon "ROUNA0"
	tag "$TAG_ROUNDS"
	states
	{
		Spawn:
			ROUN A -1
			stop
	}
}

// Rounds Case -------------------------------------------------------------

actor HacxRoundsCase : HacxRounds replaces ClipBox
{
	//game Hacx
	spawnid 139
	inventory.pickupmessage "$GOTROUNDSCASE"
	inventory.amount 100
	states
	{
		Spawn:
			RBOX A -1
			stop
	}
}

// Cartridges ---------------------------------------------------------------

actor HacxCartridges : Ammo replaces Shell
{
	//game Hacx
	spawnid 12
	inventory.pickupmessage "$GOTCARTRIDGES"
	inventory.amount 4
	inventory.maxamount 50
	ammo.backpackamount 4
	ammo.backpackmaxamount 100
	inventory.icon "CARTA0"
	tag "$TAG_CARTRIDGES"
	states
	{
		Spawn:
			CART A -1
			stop
	}
}

// Cartridge Case ---------------------------------------------------------------

actor HacxCartridgesCase : HacxCartridges replaces ShellBox
{
	//game Hacx
	spawnid 143
	inventory.pickupmessage "$GOTCARTRIDGESCASE"
	inventory.amount 20
	states
	{
		Spawn:
			CBOX A -1
			stop
	}
}

// Torpedo -----------------------------------------------------------------

actor HacxTorpedo : Ammo replaces RocketAmmo
{
	//game Hacx
	spawnid 140
	inventory.pickupmessage "$GOTTORPEDO"
	inventory.amount 1
	inventory.maxamount 50
	ammo.backpackamount 1
	ammo.backpackmaxamount 100
	inventory.icon "TMISA0"
	tag "$TAG_TORPEDOS"
	states
	{
		Spawn:
			TMIS A -1
			stop
	}
}

// Torpedo Case -------------------------------------------------------------

actor HacxTorpedoCase : HacxTorpedo replaces RocketBox
{
	//game Hacx
	spawnid 141
	inventory.pickupmessage "$GOTTORPEDOS"
	inventory.amount 5
	states
	{
		Spawn:
			TPAK A -1
			stop
	}
}

// Molecules ---------------------------------------------------------------

actor HacxMolecules : Ammo replaces Cell
{
	//game Hacx
	spawnid 75
	inventory.pickupmessage "$GOTMOLECULES"
	inventory.amount 20
	inventory.maxamount 400
	ammo.backpackamount 20
	ammo.backpackmaxamount 800
	inventory.icon "MOLCA0"
	tag "$TAG_MOLECULES"
	states
	{
		Spawn:
			MOLC A -1
			stop
	}
}

// Molecules Tank -----------------------------------------------------------

actor HacxMoleculesTank : HacxMolecules replaces CellPack
{
	//game Hacx
	spawnid 142
	inventory.pickupmessage "$GOTMOLECULESTANK"
	inventory.amount 100
	states
	{
		Spawn:
			MOLT A -1
			stop
	}
}

// Valise -----------------------------------------------------------------

ACTOR HacxValise : Backpack replaces Backpack
{
	//game Hacx
	SpawnID 144
	Inventory.PickupMessage "$GOTVALISE"
	States
	{
		Spawn:
			VALI A -1
			Stop
	}
}

// MicroKit -----------------------------------------------------------------

actor HacxMicroKit : HealthBonus replaces HealthBonus
{
	//game Hacx
	spawnid 152
	inventory.maxamount 400
	inventory.pickupmessage "$GOTMICROKIT"
	States
	{
		Spawn:
			KIT1 ABCD 4
			Loop
	}
}
	
// Inhaler -----------------------------------------------------------------

actor HacxInhaler : Stimpack replaces Stimpack
{
	//game Hacx
	spawnid 23
	inventory.pickupmessage "$GOTINHALER"
	States
	{
		Spawn:
			HALE A -1
			Stop
	}
}

// Hypo ---------------------------------------------------------------------

actor HacxHypo : Medikit replaces Medikit
{
	//game Hacx
	spawnid 24
	inventory.amount 25
	inventory.pickupmessage "$GOTHYPO"
	health.lowmessage 25, "$GOTHYPONEED"
	States
	{
		Spawn:
			HYPO A -1
			Stop
	}
}

// Dampener --------------------------------------------------------------

actor HacxDampener : ArmorBonus replaces ArmorBonus
{
	//game Hacx
	spawnid 22
	inventory.pickupmessage "$GOTDAMPENER"
	armor.maxsaveamount 400
	states
	{
		Spawn:
			KIT2 A -1
			stop
	}
}

// Kevlarvest -------------------------------------------------------------

actor HacxKevlarVest : GreenArmor replaces GreenArmor
{
	//game Hacx
	spawnid 68
	inventory.pickupmessage "$GOTKEVLARVEST"
	states
	{
		Spawn:
			KEV1 A -1
			stop
	}
}
// Given to player at map start:
actor HacxLightKevlarVest : HacxKevlarVest
{
	Armor.Saveamount 50
}

// Super Kevlarvest -------------------------------------------------------

actor HacxSuperKevlarVest : BlueArmor replaces BlueArmor
{
	//game Hacx
	spawnid 69
	inventory.pickupmessage "$GOTSKEVLARVEST"
	States
	{
		Spawn:
			KEV2 A 6
			KEV2 B 6 bright
			loop
	}
}

// 007 Microtel ------------------------------------------------------------------

ACTOR HacxMicrotel : Berserk replaces Berserk
{
	//game Hacx
	SpawnID 134
	Inventory.PickupMessage "$GOTMICROTEL"
	States
	{
		Spawn:
			MICR A -1
			Stop
		Pickup:
			TNT1 A 0 A_GiveInventory("PowerStrength")
			TNT1 A 0 HealThing(100, 0)
			TNT1 A 0 A_SelectWeapon("HacxKick")
			Stop
	}
}

// Centrophenoxine ---------------------------------------------------------

ACTOR HacxCentrophenoxine : Soulsphere replaces Soulsphere
{
	//game Hacx
	spawnid 25
	Inventory.PickupMessage "$GOTCENTROPHENOXINE"
	States
	{
		Spawn:
			DRUG ABCDEFGH 4 Bright
			Loop
	}
}


// Body Armor --------------------------------------------------------------

ACTOR HacxBodyArmorHealth : Health
{
	Inventory.Amount 400
	Inventory.MaxAmount 400
	+INVENTORY.ALWAYSPICKUP
}

actor HacxBodyArmor : Megasphere replaces Megasphere
{
	//game Hacx
	spawnid 132
	inventory.pickupmessage "$GOTBODYARMOR"
	states
	{
		Spawn:
			BODY ABCDBC 6
			loop
		Pickup:
			TNT1 A 0 A_GiveInventory("BlueArmorForMegasphere", 1)
			TNT1 A 0 A_GiveInventory("HacxBodyArmorHealth", 1)
			stop
	}
}

// Force Field -------------------------------------------------------------

actor HacxForceField : InvulnerabilitySphere replaces InvulnerabilitySphere
{
	//game Hacx
	spawnid 133
	powerup.color BlueMap
	inventory.pickupmessage "$GOTFORCEFIELD"
	states
	{
		Spawn:
			FORC ABCB 4 bright
			loop
	}
}

// Speed Hack ------------------------------------------------------------------

actor HacxSpeedHack : PowerupGiver replaces BlurSphere
{
	//game Hacx
	spawnid 135
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Powerup.Type "Speed"
	inventory.pickupmessage "$GOTSPEEDHACK"
	States
	{
		Spawn:
			ENKB ABCD 4 Bright
			Loop
	}
}

// Rubber Boots -------------------------------------------------------------

actor HacxVulcanBoots : RadSuit replaces RadSuit
{
	//game Hacx
	spawnid 136
	inventory.pickupmessage "$GOTRUBBERBOOTS"
	States
	{
		Spawn:
			BOOT A -1
			Stop
	}
}

// infrared -----------------------------------------------------------------

actor HacxInfraredVisor : Infrared replaces Infrared
{
	//game Hacx
	spawnid 138
	inventory.pickupmessage "$GOTINFRARED"
	States
	{
		Spawn:
			VISR A 6 Bright
			VISR B 6
			Loop
	}
}
// SI Array -----------------------------------------------------------------

actor HacxSIArrayMap : Allmap replaces Allmap
{
	//game Hacx
	spawnid 137
	inventory.pickupmessage "$GOTSIARRAY"
	States
	{
		Spawn:
			AMAP ABCDCB 6 Bright
			Loop
	}
}

// Key Card ------------------------------------------------------------------

actor HacxKeyCard : DoomKey replaces BlueCard
{
	//game Hacx
	spawnid 85
	inventory.pickupmessage "$GOTKEYCARD"
	inventory.icon "STKEYS0"
	states
	{
		Spawn:
			CARD A -1
			stop
	}
}

// Password ------------------------------------------------------------------

actor HacxPassword : DoomKey replaces RedCard
{
	//game Hacx
	spawnid 86
	inventory.pickupmessage "$GOTPASSWORD"
	inventory.icon "STKEYS2"
	states
	{
		Spawn:
			PASS A -1
			stop
	}
}

// Gold Key ------------------------------------------------------------------

actor HacxGoldKey : DoomKey replaces YellowCard
{
	//game Hacx
	spawnid 87
	inventory.pickupmessage "$GOTGOLDKEY"
	inventory.icon "STKEYS1"
	states
	{
		Spawn:
			GKEY A -1
			stop
	}
}

// Blue Z Key -----------------------------------------------------------

actor HacxBlueZKey : DoomKey replaces BlueSkull
{
	//game Hacx
	spawnid 90
	inventory.pickupmessage "$GOTBLUEZKEY"
	inventory.icon "STKEYS3"
	states
	{
		Spawn:
			ZKYB A 10
			ZKYB B 10 bright
			loop
	}
}

// Red Z Key ----------------------------------------------------------------

actor HacxRedZKey : DoomKey replaces RedSkull
{
	//game Hacx
	spawnid 89
	inventory.pickupmessage "$GOTREDZKEY"
	inventory.icon "STKEYS5"
	states
	{
		Spawn:
			ZKYR A 10
			ZKYR B 10 bright
			loop
	}
}

// Yellow Z Key -------------------------------------------------------------

actor HacxYellowZKey : DoomKey replaces YellowSkull
{
	//game Hacx
	spawnid 88
	inventory.pickupmessage "$GOTYELWZKEY"
	inventory.icon "STKEYS4"
	states
	{
		Spawn:
			ZKYY A 10
			ZKYY B 10 bright
			loop
	}
}


				    /* ---------- DECORATIONS ---------- */
					

// Water Drops / Rain-----------------------------------------------------------

actor HacxWaterDroplet
{
	//game Hacx
	radius 6
	height 8
	PROJECTILE
	-NOGRAVITY
	+THRUACTORS
	states
	{
		Spawn:
			DRIP A -1
			stop
		Death:
			DRIP BCDE 3
			stop
	}
}

actor HacxWaterDrip replaces TechLamp
{
	//game Hacx
	radius 16
	height 8
	+SPAWNCEILING
	+NOGRAVITY
	states
	{
		Spawn:
			TNT1 A 20
			TNT1 A 28 A_SpawnItemEx("HacxWaterDroplet",0,0,0,0,0,0,0,128,0)
			loop
	}
}

actor HacxWaterDrip2 : HacxWaterDrip replaces TechLamp2
{
	states
	{
		Spawn:
			TNT1 A 28 A_SpawnItemEx("HacxWaterDroplet",0,0,0,0,0,0,0,128,0)
			loop
	}
}


// Computer ----------------------------------------------------------------

actor HacxComputer replaces Column
{
	//game Hacx
	health 1
	radius 12
	height 27
	mass 85
	deathsound "misc/shatter"
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	states
	{
		Spawn:
			COMP A -1
			stop
		Death:
			COMP A 0 A_Scream
			COMP A 0 A_NoBlocking
		DeathLoop:
			COMP BCD 6
			goto DeathLoop
	}
}

// Hacx Android Upper Part -------------------------------------------------

actor HacxAndroidUpperPart replaces TallGreenColumn
{
	//game Hacx
	radius 15
	height 40
	health 10000
	mass 10000
	+SOLID
	+NOBLOOD
	+SHOOTABLE
	+INVULNERABLE //Prevent the object from disappering
	states
	{
		Spawn:
			AND1 A -1
			stop
	}
}

// Hacx Android Lower Part -------------------------------------------------

actor HacxAndroidLowerPart replaces ShortGreenColumn
{
	//game Hacx
	radius 15
	height 40
	health 10000
	mass 10000
	+SOLID
	+NOBLOOD
	+SHOOTABLE
	+INVULNERABLE
	states
	{
		Spawn:
			AND2 A -1
			stop
	}
}

// Flowers -----------------------------------------------------------------

actor HacxFlowers replaces TallRedColumn
{
	//game Hacx
	radius 20
	height 26
	states
	{
		Spawn:
			FLW1 A -1
			stop
	}
}

// HacxLavaBall ------------------------------------------------------------

actor HacxLavaBall
{
	//game Hacx
	radius 6
	height 8
	PROJECTILE
	-NOGRAVITY
	+THRUACTORS
	states
	{
		Spawn:
			FBAL A 0 bright			
			FBAL A 0 bright A_PlaySound("misc/fireball")
			FBAL ABA 3 bright A_SpawnItemEx("HacxPuff",0,0,0,0,0,0,0,128,0)
			FBAL BCDEF 4 bright A_SpawnItemEx("HacxPuff",0,0,0,0,0,0,0,128,0)
			wait
		Death:
			FBSH BCD 4 bright
			stop
	}
}

actor HacxLavaBallSpawner replaces ShortRedColumn
{
	//game Hacx
	radius 8
	height 16
	states
	{
		Spawn:
			TNT1 A 50
			FBSH A 4 bright
			FBSH B 4 bright A_SpawnItemEx("HacxLavaBall",0,0,0,0,0,16,0,128,0)
			FBSH CD 4 bright
			TNT1 A 50
			loop
	}
}

// HacxLavaBall2 -----------------------------------------------------------

actor HacxLavaBallSpawner2 : HacxLavaBallSpawner replaces SkullColumn
{
	//game Hacx
	-RANDOMIZE
}

// Ceiling Light -----------------------------------------------------------

actor HacxCeilingLight replaces BlueTorch
{
	//game Hacx
	radius 16
	height 16
	+SOLID
	+SPAWNCEILING
	+NOGRAVITY
	states
	{
		Spawn:
			LIT1 A -1 bright
			stop
	}
}

// Long Ceiling Light ------------------------------------------------------

actor HacxLongCeilingLight replaces RedTorch
{
	//game Hacx
	radius 16
	height 32
	+SPAWNCEILING
	+NOGRAVITY
	states
	{
		Spawn:
			LIT2 A -1 bright
			stop
	}
}

// Desk Lamp ---------------------------------------------------------------

actor HacxDeskLamp replaces ShortBlueTorch
{
	//game Hacx
	radius 20
	height 19
	health 2
	mass 10
	deathsound "misc/shatter"
	+SHOOTABLE
	+NOBLOOD
	states
	{
		Spawn:
			DLMP A -1 bright
			stop
		Death:
			DLMP B 6 A_Scream
			DLMP C 6
			DLMP D -1
			stop
	}
}

// Hacx Wall Torch ---------------------------------------------------------

actor HacxWallTorch replaces ShortGreenTorch
{
	//game Hacx
	-SOLID
	states
	{
		Spawn:
			TRCH ABCD 4 bright A_Jump(64,"AnimStart")
		AnimStart:
			TRCH A 0 bright A_PlaySound("misc/torch", CHAN_VOICE, 1.0, 1)
		AnimLoop:
			TRCH ABCD 4 bright
			loop
	}
}

// Floor Lamp --------------------------------------------------------------

actor HacxFloorLamp replaces ShortRedTorch
{
	//game Hacx
	radius 19
	height 51
	health 2
	mass 10
	deathsound "misc/shatter"
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	states
	{
		Spawn:
			LAMP A -1 bright
			stop
		Death:
			LAMP B 6 A_Scream
			LAMP C 6 A_NoBlocking
			LAMP D -1
			stop
	}
}

// Hacx Bush ---------------------------------------------------------------

actor HacxBush replaces Stalagtite
{
	//game Hacx
	radius 16
	height 32
	states
	{
		Spawn:
			BUSH A -1
			stop
	}
}

// Road Spike ----------------------------------------------------------

actor HacxRoadSpike replaces TechPillar
{
	//game Hacx
	radius 20
	height 40
	+SOLID
	states
	{
		Spawn:
			SPIK A -1
			stop
	}
}

// Samurai Statue ----------------------------------------------------------

actor HacxSamuraiStatue replaces Candlestick
{
	//game Hacx
	radius 40
	height 95
	mass 4000
	+SOLID
	states
	{
		Spawn:
			SAMU A -1 bright
			stop
	}
}

// Office Chair ------------------------------------------------------------

actor HacxOfficeChair replaces Candelabra
{
	//game Hacx
	radius 24
	height 40
	health 40
	painchance 400
	mass 30
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+PUSHABLE
	deathsound "misc/smash"
	states
	{
		Spawn:
			CHAR A -1
			stop
		Pain:
			CHAR B -1
			stop
		Death:
			CHAR C 8 A_NoBlocking
			CHAR D -1
			stop
	}
}

// Hanging Body ------------------------------------------------------------

actor HacxHangingBody replaces BloodyTwitch
{
	//game Hacx
	radius 16
	height 68
	painchance 256
	health 10000000
	mass 10000000
	+SOLID
	+NOGRAVITY
	+SPAWNCEILING
	+SHOOTABLE
	painsound "*xdeath"
	states
	{
		Spawn:
			HBOD A 10
			HBOD B 15
			HBOD C 10
			loop
		Pain:
			HBOD D 12 A_Pain
			goto Spawn
	}
}

// DeadLabTech --------------------------------------------------------------

actor HacxDeadLabTech1 replaces Meat2
{
	//game Hacx
	states
	{
		Spawn:
			DED1 A -1
			stop
	}
}

actor HacxDeadLabTech2 replaces Meat3
{
	//game Hacx
	states
	{
		Spawn:
			DED2 A -1
			stop
	}
}

actor HacxDeadLabTech3 replaces Meat4
{
	//game Hacx
	states
	{
		Spawn:
			DED3 A -1
			stop
	}
}

// More Dead Things --------------------------------------------------------

actor HacxGibs replaces Gibs
{
	//game Hacx
	states
	{
		Spawn:
			DED4 A -1
			stop
	}
}

actor DeadThug : DeadZombieMan replaces DeadZombieMan
{
	//game Hacx
	states
	{
		Spawn:
			THUG L -1
			stop
	}
}

actor DeadAndroid : DeadShotgunGuy replaces DeadShotgunGuy
{
	//game Hacx
	states
	{
		Spawn:
			ANDY L -1
			stop
	}
}

actor DeadBuzzer : DeadDemon replaces DeadDemon
{
	//game Hacx
	states
	{
		Spawn:
			BUZZ N -1
			stop
	}
}

// Bonzai Tree -------------------------------------------------------------

actor HacxBonzaiTree replaces HeadOnAStick
{
	//game Hacx
	radius 17
	height 55
	states
	{
		Spawn:
			BONZ A -1
			stop
	}
}

// Raft --------------------------------------------------------------------

actor HacxRaft replaces HeadsOnAStick
{
	//game Hacx
	radius 50
	height 25
	states
	{
		Spawn:
			RAFT A -1
			stop
	}
}

// Rickshaw ----------------------------------------------------------------

actor Rickshaw replaces HeadCandles
{
	//game Hacx
	radius 58
	height 70
	+SOLID
	states
	{
		Spawn:
			SHAW A -1
			stop
	}
}

// Sentry Lamp -------------------------------------------------------------

actor HacxSentryLamp replaces DeadStick
{
	//game Hacx
	radius 12
	height 42
	+SOLID
	states
	{
		Spawn:
			SECL A -1
			stop
	}
}

// Breakable Vase ----------------------------------------------------------

actor HacxBreakableVase replaces LiveStick
{
	//game Hacx
	radius 16
	height 48
	health 2
	mass 10
	deathsound "misc/shatter"
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	states
	{
		Spawn:
			VASE A -1
			stop
		Death:
			VASE B 7 A_Scream
			VASE C -1 A_Fall
			stop
	}
}

// Detonator ---------------------------------------------------------------

actor HacxDetonator replaces BurningBarrel
{
	//game Hacx
	spawnid 149
	radius 16
	height 32
	health 300
	mass 1000000
	+INVISIBLE
	states
	{
		Spawn:
			TNT1 A 2 A_Look
			loop
		See:
			TNT1 A 2 A_Explode
			TNT1 A 1 A_NoBlocking
			stop
		Death:
			goto See+1
	}
}

actor HacxExplosion replaces CommanderKeen
{
	//game Hacx
	radius 32
	height 72
	health 2
	mass 1000000
	+SHOOTABLE
	DeathSound "misc/explode"
	states
	{
		Spawn:
			TNT1 A -1
			loop
		Pain:
		Death:
			NUK1 A 6 bright
			NUK1 B 6 bright A_Scream
 			NUK1 C 6 bright A_Explode //(0,16)
			NUK1 DEF 6 bright A_NoBlocking
			stop
	}
}

// Nitro Barrel ------------------------------------------------------------

ACTOR HacxNitro : ExplosiveBarrel replaces ExplosiveBarrel
{
	+PUSHABLE
	DeathSound "misc/explode"
	Height 56
	//DamageFactor "Kick", 0.0
	States
	{
	Spawn:
		NITR A -1
		Stop
	Death:
		NITR B 4 BRIGHT 
		NITR C 4 BRIGHT A_Scream
		NUK1 C 5 BRIGHT A_Explode(255,255)
		NUK1 D 5 BRIGHT
		NUK1 EF 5 BRIGHT
		NUK1 F 1050 BRIGHT A_BarrelDestroy
		NUK1 F 5 A_Respawn
		Wait
	}
}

// Moss Small --------------------------------------------------------------

actor HacxMossSmall replaces HangNoGuts
{
	//game Hacx
	radius 5
	height 25
	+NOGRAVITY
	+SPAWNCEILING
	states
	{
		Spawn:
			MOS1 A -1
			stop
	}
}

// Moss Large --------------------------------------------------------------

actor HacxMossLarge replaces HangBNoBrain
{
	//game Hacx
	radius 5
	height 25
	+NOGRAVITY
	+SPAWNCEILING
	states
	{
		Spawn:
			MOS2 A -1
			stop
	}
}

// Stalagmite --------------------------------------------------------------

actor HacxStalagmite replaces HangTLookingDown
{
	//game Hacx
	radius 5
	height 32
	+SOLID
	states
	{
		Spawn:
			STG1 A -1
			stop
	}
}

// Stalactite -------------------------------------------------------------

actor HacxStalactite replaces HangTLookingUp
{
	//game Hacx
	radius 5
	height 35
	+SPAWNCEILING
	+NOGRAVITY
	states
	{
		Spawn:
			STG3 A -1
			stop
	}
}

// Stalagmite Large --------------------------------------------------------

actor HacxStalagmiteLarge replaces HangTSkull
{
	//game Hacx
	radius 5
	height 45
	+SOLID
	states
	{
		Spawn:
			STG2 A -1
			stop
	}
}

// Stalactite Large -------------------------------------------------------

actor HacxStalactiteLarge replaces HangTNoBrain
{
	//game Hacx
	radius 5
	height 45
	+NOGRAVITY
	+SPAWNCEILING
	states
	{
		Spawn:
			STG4 A -1
			stop
	}
}

// Rock --------------------------------------------------------------------

actor HacxRock replaces ColonGibs
{
	//game Hacx
	spawnid 147
	radius 5
	height 32
	states
	{
		Spawn:
			ROCK A -1
			stop
	}
}

// Cacti -------------------------------------------------------------------

actor HacxCactus1 3801
{
	//game Hacx
	+SOLID
	+BUMPSPECIAL
	radius 24
	height 72
	obituary "$OB_CACTUS"
	states
	{
		Spawn:
			CACT A 0
			CACT A 0 A_SetSpecial(73,5,0,0,0,0)
			CACT A -1
			stop
	}
}
actor HacxCactus2 : HacxCactus1 3802
{
	//game Hacx
	radius 16
	height 64
	states
	{
		Spawn:
			CACT B 0
			CACT B 0 A_SetSpecial(73,2,0,0,0,0)
			CACT B -1
			stop
	}
}

// Palm Trees --------------------------------------------------------------

actor HacxPalmTreeShort 3803
{
	//game Hacx
	+SOLID
	radius 24
	height 80
	states
	{
		Spawn:
			PALM A -1
			stop
	}
}
actor HacxPalmTreeLeanRight 3804
{
	//game Hacx
	+SOLID
	radius 24
	height 96
	states
	{
		Spawn:
			PALM B -1
			stop
	}
}
actor HacxPalmTreeLeanLeft 3805
{
	//game Hacx
	+SOLID
	radius 24
	height 96
	states
	{
		Spawn:
			PALM C -1
			stop
	}
}
actor HacxPalmTreeTall 3806
{
	//game Hacx
	+SOLID
	radius 24
	height 128
	states
	{
		Spawn:
			PALM D -1
			stop
	}
}

// Mummy -------------------------------------------------------------------

actor HacxMummy : HacxPhage replaces BrainStem
{
	//game Hacx
	spawnid 150
	radius 25
	height 96
	health 105
	speed 27
	damage 2
	painchance 150
	mass 150
	+SOLID
	+SHOOTABLE
	+JUSTATTACKED
	+DROPOFF
	states
	{
		Spawn:
			PMUM A -1
			stop
		Pain:
		See:
			PMUM B 15 bright
		Chase:
			FATT AABB 5 A_Chase
			goto Chase
	}
}

// Skeleton -----------------------------------------------------------------

actor HacxSkeleton : HacxMummy replaces SmallBloodPool
{
	//game Hacx
	spawnid 148
	radius 27
	seesound "fatso/sight" 
	attacksound "imp/melee"
	painsound "fatso/pain"
	deathsound "fatso/death"
	activesound "skeleton/death"
	states
	{
		Spawn:
			PSKE A -1
			stop
		Pain:
		See:
			PSKE B 15 bright
		Chase:
			FATT AABB 5 A_Chase
			goto Chase
	}
}

// Pulsar ----------------------------------------------------------------------

actor HacxPulsar replaces EvilEye
{
	//game Hacx
	radius 16
	height 16
	states
	{
		Spawn:
			PULS A 15
			PULS B 15 A_PlaySound("misc/pulse")
			PULS C 20 bright A_Explode
			loop
	}
}

// C-Eye -----------------------------------------------------------------------

actor HacxCEye replaces FloatingSkull
{
	//game Hacx
	radius 64
	height 128
	health 200
	mass 10000000
	+SOLID
	+SHOOTABLE
	deathsound "cyber/explode"
	bloodtype "HacxDigitYellow"
	renderstyle translucent
	alpha 0.8
	states
	{
		Spawn:
			CEYE ABCD 5 bright
			loop
		Death:
			TNT1 A 0 bright A_Scream
 			TNT1 A 1 bright A_Burst("HacxDigitYellow")
			Stop
	}
}

// Electron --------------------------------------------------------------------

actor HacxElectron replaces GreenTorch
{
	//game Hacx
	radius 32
	height 96
	damage 40
	health 70
	mass 20
	+SOLID
	+SHOOTABLE
	+NOGRAVITY
	+DROPOFF
	+NOBLOOD
	+PUSHABLE
	deathsound "misc/explode"
	renderstyle translucent
	Alpha 0.8
	states
	{
	Spawn:
		TRON ABCDEFGHIJKLMNOP 2 bright
		loop
	Death:
		NUK1 A 0 bright A_Scream
		NUK1 A 0 bright A_Explode(128,256)
		NUK1 A 0 bright A_SpawnItemEx("HacxElectronExplode",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION,0)
		NUK1 A 0 bright A_Burst("HacxDigitYellow")
		Stop
	}
}
Actor HacxElectronExplode : HacxTrail
{
	Alpha 1.0
	Renderstyle Add
	States
	{
	Spawn:
		NUK1 BCDEF 5 BRIGHT
		Stop
	}	
}

// C-Space Files ---------------------------------------------------------------

actor HacxFileWhite 3901
{
	//game Hacx
	radius 12
	height 48
	health 50
	mass 25
	+SOLID
	+SHOOTABLE
	+PUSHABLE
	bloodtype "HacxDigitWhite"
	deathsound "cyber/hit"
	renderstyle translucent
	alpha 0.8
	states
	{
	Spawn:
		CFIL A -1 bright
		stop
	Death:
		TNT1 A 0 bright A_Scream
 		TNT1 A 1 bright A_Burst("HacxDigitWhite")
		Stop
	}
}
actor HacxFileGreen : HacxFileWhite 3902
{
	bloodtype "HacxDigitGreen"
	states
	{
	Spawn:
		CFIL B -1 bright
		stop
	Death:
		TNT1 A 0 bright A_Scream
 		TNT1 A 1 bright A_Burst("HacxDigitGreen")
		Stop
	}	
}
actor HacxFileBlue : HacxFileWhite 3903
{
	bloodtype "HacxDigitBlue"
	states
	{
	Spawn:
		CFIL C -1 bright
		stop
	Death:
		TNT1 A 0 bright A_Scream
 		TNT1 A 1 bright A_Burst("HacxDigitBlue")
		Stop
	}	
}
actor HacxFileRed : HacxFileWhite 3904
{
	bloodtype "HacxDigitRed"
	states
	{
	Spawn:
		CFIL D -1 bright
		stop
	Death:
		TNT1 A 0 bright A_Scream
 		TNT1 A 1 bright A_Burst("HacxDigitRed")
		Stop
	}	
}
actor HacxFileYellow : HacxFileWhite 3905
{
	bloodtype "HacxDigitYellow"
	states
	{
	Spawn:
		CFIL E -1 bright
		stop
	Death:
		TNT1 A 0 bright A_Scream
 		TNT1 A 1 bright A_Burst("HacxDigitYellow")
		Stop
	}	
}

// Digit Particles -------------------------------------------------------------

actor HacxDigitWhite
{
	//game Hacx
	radius 2
	height 2
	alpha 0.8
	gravity 0.75
	renderstyle translucent
	+NOBLOCKMAP
	+NOTELEPORT
	+THRUACTORS
	states
	{
	Spawn:
		CDG1 AAA 0 Bright
		Goto SelectState
	SelectState:
		"####" A 0 Bright A_Jump(256,1,2,3,4)
		"####" A 6 Bright
		Goto Fadeout
		"####" B 6 Bright
		Goto Fadeout
		"####" C 6 Bright
		Goto Fadeout
		"####" D 6 Bright
		Goto Fadeout
	Fadeout:
		"####" # 1 Bright A_FadeOut(0.1,1)
		Loop
	}
}
actor HacxDigitGreen : HacxDigitWhite
{
	states
	{
	Spawn:
		CDG2 AAA 0 Bright
		Goto SelectState
	}
}
actor HacxDigitBlue : HacxDigitWhite
{
	states
	{
	Spawn:
		CDG3 AAA 0 Bright
		Goto SelectState
	}
}
actor HacxDigitRed : HacxDigitWhite
{
	states
	{
	Spawn:
		CDG4 AAA 0 Bright
		Goto SelectState
	}
}
actor HacxDigitYellow : HacxDigitWhite
{
	states
	{
	Spawn:
		CDG5 AAA 0 Bright
		Goto SelectState
	}
}


                     /* ---------- AMBIENCE ---------- */

// Ambient Loops ---------------------------------------------------------------

ACTOR HacxAmbientLoop
{
	//game Hacx
	+NOBLOCKMAP
	+NOSECTOR
	+DONTSPLASH	
	States
	{
	Spawn:
		TNT1 A 2 A_LoopActiveSound
		Loop
	}
}
ACTOR HacxAmbienceBubbles1    : HacxAmbientLoop 3501 { ActiveSound "ambience/bubbles1" }
ACTOR HacxAmbienceBubbles2    : HacxAmbientLoop 3502 { ActiveSound "ambience/bubbles2" }
ACTOR HacxAmbienceBubbles3    : HacxAmbientLoop 3503 { ActiveSound "ambience/bubbles3" }
ACTOR HacxAmbienceBubbles4    : HacxAmbientLoop 3504 { ActiveSound "ambience/bubbles4" }
ACTOR HacxAmbienceDarkCircuit : HacxAmbientLoop 3505 { ActiveSound "ambience/darkcircuit" }
ACTOR HacxAmbienceFireSmall   : HacxAmbientLoop 3506 { ActiveSound "ambience/firesmall" }
ACTOR HacxAmbienceLightBuzz   : HacxAmbientLoop 3507 { ActiveSound "ambience/lightbuzz" }
ACTOR HacxAmbienceMotor       : HacxAmbientLoop 3508 { ActiveSound "ambience/motor" }
ACTOR HacxAmbiencePulse       : HacxAmbientLoop 3509 { ActiveSound "ambience/pulse" }
ACTOR HacxAmbienceStream      : HacxAmbientLoop 3510 { ActiveSound "ambience/stream" }
ACTOR HacxAmbienceWave1       : HacxAmbientLoop 3511 { ActiveSound "ambience/wave1" }
ACTOR HacxAmbienceWave2       : HacxAmbientLoop 3512 { ActiveSound "ambience/wave2" }
ACTOR HacxAmbienceWaterfall   : HacxAmbientLoop 3513 { ActiveSound "ambience/waterfall" }
ACTOR HacxAmbienceWindHigh    : HacxAmbientLoop 3514 { ActiveSound "ambience/windhigh" }
ACTOR HacxAmbienceWindLow     : HacxAmbientLoop 3515 { ActiveSound "ambience/windlow" }
ACTOR HacxAmbienceWindSand    : HacxAmbientLoop 3516 { ActiveSound "ambience/windsand" }
ACTOR HacxAmbienceBeep1       : HacxAmbientLoop 3517 { ActiveSound "ambience/beep1" }
ACTOR HacxAmbienceFanLarge    : HacxAmbientLoop 3518 { ActiveSound "ambience/fanlarge" }
ACTOR HacxAmbienceStatic1     : HacxAmbientLoop 3519 { ActiveSound "ambience/static1" }
ACTOR HacxAmbienceCave1       : HacxAmbientLoop 3520 { ActiveSound "ambience/cave1" }
ACTOR HacxAmbienceCave2       : HacxAmbientLoop 3521 { ActiveSound "ambience/cave2" }
ACTOR HacxAmbienceFire1       : HacxAmbientLoop 3522 { ActiveSound "ambience/fire1" }
ACTOR HacxAmbienceFire2       : HacxAmbientLoop 3523 { ActiveSound "ambience/fire2" }
ACTOR HacxAmbienceFire3       : HacxAmbientLoop 3524 { ActiveSound "ambience/fire3" }
ACTOR HacxAmbienceFire4       : HacxAmbientLoop 3525 { ActiveSound "ambience/fire4" }
ACTOR HacxAmbienceLava1       : HacxAmbientLoop 3526 { ActiveSound "ambience/lava1" }
ACTOR HacxAmbienceLava2       : HacxAmbientLoop 3527 { ActiveSound "ambience/lava2" }
ACTOR HacxAmbienceLava3       : HacxAmbientLoop 3528 { ActiveSound "ambience/lava3" }


// Point Sounds ----------------------------------------------------------------

ACTOR HacxPointSound
{
	// game Hacx
	Radius 8
	Height 16
	Mass 0x7FFFFFF
	+VULNERABLE
	+TOUCHY
	States
	{
	Spawn:
		TNT1 A 4
		Loop
	Death:
		TNT1 A 1 A_ActiveSound
		Stop
	}
}
ACTOR HacxSoundPowerDown : HacxPointSound 3701 { ActiveSound "point/powerdown" }
ACTOR HacxSoundPowerUp   : HacxPointSound 3702 { ActiveSound "point/powerup" }
ACTOR HacxSoundDitty     : HacxPointSound 3703 { ActiveSound "point/ditty" }


// Rain Effects ----------------------------------------------------------------

actor HacxRainDroplet
{
	//game Hacx
	radius 6
	height 8
	+NOINTERACTION
	states
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(z >= ceilingz, "Vanish")
			TNT1 A 1 A_SpawnItemEx("HacxWaterDroplet",0,0,(ceilingz-z)-8.0,0,0,0,0,128,0)
			Stop
		Vanish:
			TNT1 A 0
			Stop
	}
}

// I'm sure some people would want to murder me for this one-line
// shite, but vertical space is precious in the Xasermind.

actor HacxRainLight64  : HacxWaterDrip 3601 { states { Spawn: TNT1 A 8    A_SpawnItemEx("HacxRainDroplet",random(-32,32)  ,random(-32,32)  ,0,0,0,0,0,128,0) loop } }
actor HacxRainLight128 : HacxWaterDrip 3602 { states { Spawn: TNT1 A 4    A_SpawnItemEx("HacxRainDroplet",random(-64,64)  ,random(-64,64)  ,0,0,0,0,0,128,0) loop } }
actor HacxRainLight256 : HacxWaterDrip 3603 { states { Spawn: TNT1 A 2    A_SpawnItemEx("HacxRainDroplet",random(-128,128),random(-128,128),0,0,0,0,0,128,0) loop } }

actor HacxRainMedium64 : HacxWaterDrip 3604 { states { Spawn: TNT1 A 4    A_SpawnItemEx("HacxRainDroplet",random(-32,32)  ,random(-32,32)  ,0,0,0,0,0,128,0) loop } }
actor HacxRainMedium128: HacxWaterDrip 3605 { states { Spawn: TNT1 A 2    A_SpawnItemEx("HacxRainDroplet",random(-64,64)  ,random(-64,64)  ,0,0,0,0,0,128,0) loop } }
actor HacxRainMedium256: HacxWaterDrip 3606 { states { Spawn: TNT1 A 1    A_SpawnItemEx("HacxRainDroplet",random(-128,128),random(-128,128),0,0,0,0,0,128,0) loop } }

actor HacxRainHeavy64  : HacxWaterDrip 3607 { states { Spawn: TNT1 A 1    A_SpawnItemEx("HacxRainDroplet",random(-32,32)  ,random(-32,32)  ,0,0,0,0,0,128,0) loop } }
actor HacxRainHeavy128 : HacxWaterDrip 3608 { states { Spawn: TNT1 AA 0   A_SpawnItemEx("HacxRainDroplet",random(-64,64)  ,random(-64,64)  ,0,0,0,0,0,128,0)
                                                         TNT1 A 1
														 loop } }
actor HacxRainHeavy256 : HacxWaterDrip 3609 { states { Spawn: TNT1 AAAA 0 A_SpawnItemEx("HacxRainDroplet",random(-128,128),random(-128,128),0,0,0,0,0,128,0)
                                                         TNT1 A 1
														 loop } }


// Miscellaneous stuff ---------------------------------------------------------

ACTOR EESkyboxCam : SkyCamCompat 5006 {}
